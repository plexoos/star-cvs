* ********************************************************************
* run_gstar.kumac
* 
* This kumac reads from a file and runs gstar then g2t.
* The input file can be from an event generator or a txt file 
* OR: can enter values by hand and use "phase" - use intype=phase
* 
* *****************************************************************
* ***** IMPORTANT NOTE *************
* - See also notes from Pavel Nevski on his WWW page about this:
* - The extension of the input file (first letter) tells Geant
*   what type of file it is and can therefore read it correctly.
*    .t is text file
*    .x is xdf file - if you are reading directly from xdf file use this
*    .s is staf tables  - if you are using commands to write a staf table
*                   to use for geant OR to read from a staf table file,
*                   then use this extension.
* 
******************************************************************
* 
* created 6/5/98 Kathy Turner
* editted 6/8/98 Kathy Turner - put tables in correct directories
*                             - and make so can enter values by hand
* editted 6/9/98 Kathy Turner - don't need to move directories anymore
* editted 6/26/98 Kathy Turner - now open input file using "dummy" - now
*                                you must make sure that you have correct
*                                extension on your file! (see note below)
* editted 7/8/98 Kathy Turner - put in default STAR geometry
*                               and make kumac more standard
* ******************************************************************
* input_data_dir=/star/u2d/kathy/jun98_daq/datafiles/ _
* input_file=muons_central_2.txt _
* input_data_dir=/star/mds/data/SD98/auau200/evg/central/hijing/set0001/regular/ _
* input_file=auau_ce_b0-2_1_200.xdf _
* 
 macro run__
   TOP=/scr22/kathy/testdev _
   domain='geometry sim/gstar sim/g2t' _
   phystype=off _
   geomtype=tpc_nofield _   
   num_events=1 _
   debugger=on _
   intype=file _
   input_data_dir=/star/u2d/kathy/jun98_daq/datafiles/ _
   input_file=muons_central_2.txt _
   output_file=auau_central_2.xdf
 
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_gstar
* 
   
  message ' *** TOP directory to search for kumacs --> ' [TOP]
  message ' *** domains to load  -->                   ' [domain]
  message ' *** physics type -->                       ' [phystype]
  message ' *** geometry type -->                      ' [geomtype]
  message ' *** Num Events to process -->              ' [num_events]
  message ' *** debugger on/off -->                    ' [debugger]
  message ' *** input type: phase, file -->            ' [intype]
  message ' *** Input Data directory -->               ' [INPUT_DATA_DIR]
  message ' *** Input file -->                         ' [input_file]
  message ' *** output file -->                        ' [output_file]

* ----------- General setup stuff ------------------------*
  macro/global/create TOP              [TOP]
    
* setup default directories to search for kumacs in
* setup default table directory structure in STAF

   exec $STAR/kumacs/chain/bfc_util#bfc_defaults [domain]
   exec setup#setup
   macro/global/import *
     
* --------------------------------------------------------------
* must initialize before make!
* ---------------------------------------------------------------
* initialize physics type:
     exec set_hadr_phys_[phystype]
     message ' gstar: set hadronic physics ' [phystype]
     
* initialize geometry
     if [geomtype].eq.all then 
       message ' gstar: load star geometry - all '
       exec load_star_geom#all
     elseif [geomtype].eq.tpc_nofield then
       message ' gstar: load star geometry - tpc only, no mag.field '
       exec load_star_geom#tpc_nofield
     else 
         message ' gstar: geometry type not available!! '
     endif
       
* only if doing g2t: tell g2t where to put tables
    detp g2tmain gttc.rdir=0 edir=event/geant
    
* ----------------------------------------------
       
 if $vexist(make_done)=0 then
   message "Load domains:" [domain]
   test = [domain]
   nw = $words([test])
   do i = 1, [nw]
     mod = $word([test],[i],1)
     n = $words([mod],'/') 
     module = $word([mod],[n],[n],'/')
     test_done = [module]_done
     if ($vexist([test_done])=0) then
       vec/cre [test_done]
         message ' doing make ' [mod]
         make [mod]
       if $iquest(1)<>0 then
            message "<bfc> Shared libraries have not been loaded : ABend"
       endif
     endif
     if $iquest(1)<>0 then
       message "<bfc> Shared libraries have not been loaded : ABend"
     endif
   enddo
   vec/cre make_done
 endif
 
 
* --------------------------------------------------------------
* if reading gstar from tape (file) or running gstar
* don't need if I'm reading from xdf file
* write geometry tables
 tables ' ' run/geometry 
 
 
******** turn debugger on just for GSTAR! ******************************
 if [debugger].eq.on then
   debug on
 endif
    
****************** OPEN OUTPUT FILE *******************
 exec write_table_to_file#init outunit [output_file]
 
*********** WRITE RUN INFO TO OUTPUT FILE ***************
* write Run information to output file - already in staf format 
 exec write_table_to_file#write outunit [run]/geometry 
 
************* FOR DEBUGGING *********************************
*  just for looking at output of gstar
 if [debugger].eq.on then
  swit 2 3
  next; dcut cave z 225 10 10 .04 .04
 endif
 

* ************ OPEN INPUT FILE *********************************
* open input file - this in in GEANT format!
 
 if [intype].eq.file then
   user/input dummy [input_data_dir][input_file]
   message ' use input file to run '
 elseif [intype].eq.phase then
   message ' enter particles to generate by hand '
 else
   message 'invalid input type'
 endif
 
 
*------------------- process events ------------------------------*
  
 do i = 1,[num_events]
   message ' run_gstar: processing event  '  [i]  
* clear all event tables
  tabclear [event]
* if inputting values by hand:
     if [intype].eq.phase then
       exec gstar_input_phase#init
      endif
*  do for debugging:
     if [debugger].eq.on then 
       next; dcut cave z 225 10 10 .04 .04
     endif
* run gstar/g2t - automatically reads next event from input file
*               - runs gstar, then g2t
     trig 1
     for/call g2t
* write out to output_file
 exec write_table_to_file#write outunit [geant]
 enddo
   
* -------------------- end stuff -----------------------------*
   
   exec write_table_to_file#close outunit
* 
   
  return
* ***************************************************************















