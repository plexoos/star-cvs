* created 6/5/98 Kathy Turner
* editted 6/8/98 Kathy Turner - put tables in correct directories
*                             - and make so can enter values by hand
* editted 6/9/98 Kathy Turner - don't need to move directories anymore
* 
* **** 
* This kumac reads from a file and runs gstar then g2t.
* The input file can be from an event generator or a txt file 
* OR: can enter values by hand and use "phase" - use intype=phase
* 
* ************************************************************
* ***** IMPORTANT NOTE *************
* - See also notes from Pavel Nevski on his WWW page about this:
* - The extension of the input file (first letter) tells Geant
*   what type of file it is and can therefore read it correctly.
*    .t is text file
*    .x is xdf file - if you are reading directly from xdf file use this
*    .s is staf tables  - if you are using commands to write a staf table
*                   to use for geant OR to read from a staf table file,
*                   then use this extension.
* 
******************************************************************
macro run__
   nev=1 _
   debugger=on _
   input_file=muons_central_2.txt _
   intype=file _
   output_file=muons_central_2.xdf _
   phystype=muons _
   geomtype=muons 

  
  
* *********SETUP SEARCH DIRECTORIES TO FIND KUMACS *********
    
DEFAULT = _
./kumacs/util,_
./kumacs/chain,_
./kumacs/tpc,_
./kumacs/sim,_
./kumacs/gen,_
./pams/emc/kumac,_
./pams/global/kumac,_
./pams/svt/kumac,_
./pams/tpc/kumac,_
./pams/gen/kumac,_
./pams/sim/kumac,_
$STAR/kumacs/util,_
$STAR/kumacs/chain,_
$STAR/kumacs/tpc,_
$STAR/kumacs/sim,_
$STAR/kumacs/gen,_
$STAR/kumacs/svt,_
$STAR/pams/ctf/kumac,_
$STAR/pams/emc/kumac,_
$STAR/pams/ftpc/kumac,_
$STAR/pams/gen/kumac,_
$STAR/pams/global/kumac,_
$STAR/pams/mwc/kumac,_
$STAR/pams/svt/kumac,_
$STAR/pams/tpc/kumac,_
$STAR/pams/trg/kumac,_
$STAR/pams/sim/kumac 
 
def [DEFAULT]
 message 'Define default kumac search path as' def
 
 
* turn debugger on just for GSTAR! ******************************
 if [debugger].eq.on then
   debug on
 endif
 
* ***************** SETUP TABLE DIRECTORIES ******************
  exec setup_dir 
  
* ***************** OPEN OUTPUT FILE *******************
 exec write_table_to_file#init outunit [output_file]
 
 
 
* ******* INITIALIZATION & LOADING ************************
* ******* Note: Geant library loading slightly different than other software*
 exec gstar#init_phys [phystype] 
 exec gstar#init_geom [geomtype]
 exec gstar#load_lib
 

******** INITIALIZATION AFTER LOADING ************************
*  nothing now
 
 
* *********** FOR DEBUGGING *********************************
*  just for looking at output of gstar
 if [debugger].eq.on then
  swit 2 3
  next; dcut cave z 225 10 10 .04 .04
 endif
 

* ********** WRITE RUN INFO TO OUTPUT FILE ***************
* write Run information to output file - already in staf format 
   exec write_table_to_file#write outunit /dui/geant/Run




* ************ OPEN INPUT FILE *********************************
* open input file - this in in GEANT format!
 
 if [intype].eq.file then
   user/input tx [input_file]
   message ' use input file to run '
 elseif [intype].eq.phase then
   message ' enter particles to generate by hand '
 else
   message 'invalid input type'
 endif
    
*************** LOOP OVER EVENTS *****************************
  
   do iev = 1,[nev]
     
* if inputting values by hand:
     if [intype].eq.phase then
       exec gstar#input_phase
      endif
 
*  do for debugging:
     if [debugger].eq.on then 
       next; dcut cave z 225 10 10 .04 .04
     endif
     
* run gstar/g2t - automatically reads next event from input file 
     exec gstar#process_event
     

* WRITE OUT EVENT INFORMATION
     exec write_table_to_file#write outunit /dui/geant/Event

     
   enddo
   
******************* CLOSE OUTPUT ***************************
   
   exec write_table_to_file#close outunit
* 
* ***********************************************************
   
  return
















