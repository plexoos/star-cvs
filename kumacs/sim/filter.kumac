macro filter
 option  stat
 option  date
 option  nbox
 filecase keep
 pwd =$shell('pwd');             message pwd  = [pwd]
 name=$shell('basename `pwd`');  message name = [name]
 ve/cr    id(3)
 ve/read  id  N
 nn=id(1)
 n1=id(2)
 n2=id(3)
 mm=0
 cnt=0
 cno=0
 nev=0
 done=0
 len=999999
 message first file = [n1], last file = [n2], output [name]

 ghist [name].his
 gfile pz gsrun.[n1].fz
 make  sim/control
 geom=$iquest(100)
 if ([geom].le.0) then
   gfile pz geometry.fz
 endif
 make  sim/gstar
 title=merging runs [n1]-[n2] in [name]
 fort/file 66 [name].ps;  meta 66 -111
 next; dcut cave x .1 10 10 .03 .03
 Set DMOD 1; Igset TXFP -60; Igset CHHE .35 
 ITX 5 19.5  $quote([title])
 ITX .1  .1  $quote([pwd])
*
 do ni=[n1],[n2]
    file=gsrun.[ni].fz; if ($fexist([file]).eq.0) goto nextl
    gfile p [file]
    if ([len].gt.950)  then
       mm=[mm]+1
       output=[name]_0[mm]
       message new output in [output]
       gfile o [output].fzd
       if ([nev].gt.0) then; i=$shell([cmv]);  endif
       cno=[cnt]
    endif 

    call rzcdir('//SLUGRZ',' ')
    trig 100

    cnt=$iquest(100)
    nev=[cnt]-[cno]
    cmv=mv [output].fzd ../[output]_[nev]evts.fzd
    message file [ni] cnt = [cnt] done
  * get the output file length in MB:
    cmd=ls -s [output].fzd
    len=$word($shell([cmd]))
    len=[len]/2000
    nextl:
 enddo

* close the last file:
 gfile o dummy a
 if ([nev].gt.0) then; i=$shell([cmv]);  endif

* control histogram
 cdir //pawc
 tit=files [n1] - [n2] in set [name] 
 title_global $quote([tit])
 next; size 20.5 26; zone 1 4; hi/pl 11;  hi/pl 12;  hi/pl 13;  hi/pl 14
 next; zone 2 2; n/pl 1.ntrack; n/pl 1.Nvertx; n/pl 1.NtpcHit; n/pl 1.Ntr10
 close 66; meta 0
 physi
 exit  
return
         
