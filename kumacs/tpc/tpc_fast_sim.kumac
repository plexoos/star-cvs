*tfs.kumac
*----------------------------------------------------------------------
* this routine is the generic "g2t tables to tpc tracks" macro
* intermediate data are not saved
* modules involved: tfs tpt tpg
* ----------------------------------------------------------------------
*  inputfile=/star/tpca/sakrejda/gstar/auau_hij_tfs_20.xdf
macro init _
  inputfile=/star/tpca/sakrejda/gstar/test_protons.xdf

*
trace on
*
message IN MACRO INIT
message inputfile = [inputfile]


* Setup geometry files
exec $STAR_REF/sim/tss/wrk/read_geometry
newtable Geometry/tpgpar/tpg_pad tpg_pad 1
*
* initialize the geometry package
ami/module/call tpg_main Geometry/tpgpar/tpg_pad_plane _
                  Geometry/tpgpar/tpg_detector _
                  Geometry/tpgpar/tpg_pad
*
*Take care of various switches
mkdir Switches
*
*
* Define some Cluster-Finder Switches
cd  /dui/Switches
exec $STAR_REF/ana/tcl/wrk/index_type.kumac
cd  /dui

*set tfs parameters
cd  /dui/Switches
/DIO/NEWFILESTREAM tfspar $STAR_REF/sim/tfs/wrk/Staf_tfspar.xdf R
/DIO/STREAM/GETEVENT tfspar
/DIO/STREAM/close tfspar

*
* initialise switches and parameters for tracking
cd /dui/Switches
mkdir tptpar
cd tptpar
exec $STAR_REF/ana/tpt/wrk/tpt_pars.kumac

*
* initialize dedx calculation switches
* prepare for the de/dx calculation
cd /dui/Switches
mkdir tidpar
cd tidpar
exec  $STAR_REF/ana/tid/wrk/tid_pars.kumac
cd /dui

* initialise the evaluation switches
cd /dui/Switches
mkdir ttepar
cd ttepar
newtable tte_control tte_control 1
rowcount tte_control 0
*** make place to keep the input
cd /dui
dui/mkdir G2tOutput


*** open input streams
message Opening input file: [inputfile]
newfilestream G2Tfile [inputfile] r

*** read first "non-information" event (from G2T)
stream/getevent G2Tfile /dui/G2tOutput

*** Make  tables
exec tfs#make_tables

return

* ----------------------------------------------------------------------
* Create necessary tables for running TFS
* ----------------------------------------------------------------------
macro make_tables

* get ready output tables from tph
cd /dui
dui/mkdir ProducedData
dui/mkdir ProducedData/Hits
cd ProducedData/Hits
newtable tphit     tcl_tphit     400000
newtable index     tcl_tpc_index 700000

* get ready output tables from tpt
cd /dui/ProducedData
dui/mkdir Tracks
cd Tracks
newtable tptrack tpt_track 20000
cd /dui

* make the evaluation tables
cd ProducedData
mkdir Eval
cd Eval
newtable mctrk    tte_mctrk 20000
newtable evaltrk  tte_eval  20000
rowcount mctrk 0
newtable res      tte_res 1
rowcount res   1
cd /dui

hi/file 1 evaluation.nt ! n
newcwnt 111 /dui/ProducedData/Eval/mctrk

return

*.................................
* macro to loop over events!
*..................................

macro loop nevt=1 mcflag=off

message Looping from eventover [nevt] events....
do i= 1, [nevt]
  message Reading event [i]...
stream/getevent G2Tfile /dui/G2tOutput
*
* (0) clear all output tables first!
*
  cd /dui
  tdm/table/rowcount ProducedData/Hits/tphit 0
  tdm/table/rowcount ProducedData/Hits/index 0
  tdm/table/rowcount ProducedData/Tracks/tptrack 0
  tdm/table/rowcount ProducedData/Eval/mctrk 0
  tdm/table/rowcount ProducedData/Eval/evaltrk 0
  tdm/table/rowcount /dui/ProducedData/Eval/res 0
*
*fast simulator
ami/module/call tfs_g2t /dui/G2tOutput/Event/g2t_tpc_hit _
                 /dui/G2tOutput/Event/g2t_track_
                 /dui/Switches/tfspar/tfs_fspar _
                 /dui/Switches/tfspar/tfs_bmpar _
                 /dui/Switches/tfspar/tfs_fsctrl _
                 /dui/ProducedData/Hits/index /dui/Switches/type _
                 /dui/ProducedData/Hits/tphit

*hit filtering
ami/module/call tfs_filt /dui/ProducedData/Hits/tphit

*tracking
if [mcflag]=mc then
ami/module/call tte_track /dui/ProducedData/Tracks/tptrack_
                   /dui/ProducedData/Hits/tphit_
                   /dui/G2tOutput/Event/g2t_tpc_hit _
                   /dui/G2tOutput/Event/g2t_track_
                   /dui/ProducedData/Hits/index /dui/Switches/type
else
ami/module/call tpt Switches/tptpar/tpt_pars Geometry/tpgpar/tpg_pad_plane _
            /dui/ProducedData/Hits/tphit /dui/ProducedData/Tracks/tptrack
endif
*
*call the de/dx calculation
ami/module/call tde_new Switches/tidpar/tdeparm _
                 /dui/ProducedData/Hits/tphit_
                 /dui/ProducedData/Tracks/tptrack_
                 /dui/Geometry/tpgpar/tpg_pad_plane

*evaluation
ami/module/call tte /dui/ProducedData/Tracks/tptrack_
             /dui/ProducedData/Hits/tphit_
             /dui/G2tOutput/Event/g2t_tpc_hit _
             /dui/G2tOutput/Event/g2t_track_
             /dui/ProducedData/Hits/index_
             /dui/Switches/type _
             /dui/ProducedData/Eval/evaltrk /dui/ProducedData/Eval/mctrk _
             /dui/ProducedData/Eval/res  _
             /dui/Switches/ttepar/tte_control
trace on
cdir //LUN1
cwn/append 111 /dui/ProducedData/Eval/mctrk
enddo

return

macro end
cdir //LUN1
hrout 0
close 1
return





