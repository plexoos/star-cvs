** *************************************************************************
* run_tpc_slow_sim_tracker_old.kumac
* 
* created May 27, 1998 - Kathy Turner - runs tpc slow simulator & does tracking
* editted June 4, 1998 Kathy Turner
* editted 6/9/98 Kathy Turner
* editted 6/25/98 Lidia Didenko
* editted 6/25/98 Kathy Turner
* 7/2/98 - K. Turner - code now requires that you load geometry libraries 
* 7/3/98 - K. Turner - make to use bfc_util kumacs and include pam kumacs
* 7/11/98 - K. Turner - change default data file to new version
* 7/14/98 - K. Turner - added global macros
* ***********************************************************************
************************************************************************
 macro run__
          TOP=$STAR _
          domain='geometry tpc' _ 
          INPUT_DATA_DIR=$AFS_RHIC/star/data/samples/ _
          input_g2t_file=auau_central_hijing.xdf_
          num_events=1_
          tpc_sector_first=18_
          tpc_sector_last=18_
          tracker=tpt _
          funit=21 _
          nfile=tphit.ntup _
          nid=111_
          table=tpc/hits/tphit_
          adcxyzon=0
  
  
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_tpc_slow_sim_tracker_old
*
*
  message ' *** TOP directory to search for kumacs --> ' [TOP]
  message ' *** domains to load  -->                   ' [domain]
  message ' *** Data sample directory -->  ' [INPUT_DATA_DIR]
  message ' *** Input G2T file -->         ' [input_g2t_file]
  message ' *** Num Events to process -->  ' [num_events]
  message ' *** Sector first,last -->  ' [tpc_sector_first],[tpc_sector_last]
  message ' *** tpc tracker --->           ' [tracker] 
  message ' *** ntup file unit -->         ' [funit]
  message ' *** output Ntuple filename  -->' [nfile]
  message ' *** Ntuple ID # -->            ' [nid]
  message ' *** Table to write to ntup --> ' [table]
  message ' *** Fill adcxyz table for diag.? 0,1 = no,yes --> ' [adcxyzon]
  

***********************************************************
* General Setup
***********************************************************
  
 macro/global/create TOP              [TOP]

    
* setup default directories to search for kumacs in
* setup default table directory structure in STAF
* load libraries [domain]
    
   exec $STAR/kumacs/chain/bfc_util#bfc_defaults [domain]
   exec setup#setup
   macro/global/import *  
      
   if $vexist(make_done)=0 then
   message "Load domains:" [domain]
   test = [domain]
   nw = $words([test])
   do i = 1, [nw]
     mod = $word([test],[i],1)
     n = $words([mod],'/') 
     module = $word([mod],[n],[n],'/')
     test_done = [module]_done
     if ($vexist([test_done])=0) then
       vec/cre [test_done]
         message ' doing make ' [mod]
         make [mod]
       if $iquest(1)<>0 then
            message "<bfc> Shared libraries have not been loaded : ABend"
       endif
     endif
     if $iquest(1)<>0 then
       message "<bfc> Shared libraries have not been loaded : ABend"
     endif
   enddo
   vec/cre make_done
 endif
 
  
* **************************************************************
* --------- Setup and run tpc slow simulator & tracker pams -------------*
   
* open input g2t data file
  exec input_g2t_file#init [INPUT_DATA_DIR][input_g2t_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile]     
    
* --------------------- initialization ----------------------------* 

* tpc geometry init
  exec tpg_init#init

*  tpc slow sim init
    exec create_tables_tpc#tss_old
    if [adcxyzon].eq.1 then
      exec create_tables_tpc#tfc
    endif
    exec tss_init#init [tpc_sector_first] [tpc_sector_last] 

* tpc cluster init
   exec create_tables_tpc#tcl_slow_old
   exec tcl_init#init  
     
* tpc tracker init
   exec create_tables_tpc#tpt
   exec tpt_init#init
   exec tid_init#init

  
* ------------------- process events ------------------------------*
  
* read event and then process thru tpc slow sim
  do i=1,[num_events]
    message 'run_tpc_slow_sim_tracker_old: processing event ' [i]
* zero all tables
    tabclear [event]
* read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
* slow simulator
    exec tss_run#run_old
    if [adcxyzon].eq.1 then
      exec tfc_run#run 
    endif
* clustering
    exec tcl_run#run_tcl_make_clusters
    exec tcl_run#run_tpham
*  tracking
    if [tracker]=tpt then
      exec tpt_run#run
      exec tid_run#run_tde_new      
    elseif [tracker]=tte then
      exec tte_run#run_tte_track
      exec tid_run#run_tde_new
    endif
* write table to ntuple 
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]
  enddo
  
  
* ------------------- end stuff -----------------------------*
  
  ENDOFDATA:
    if [i].lt.[num_events] then
     message 'End of input file'
    endif

* close ntuple
  message 'close ntuple'
  exec write_table_to_ntuple#close_disk [funit]

* close input file
  message 'close input file'
  exec input_g2t_file#close

return

************************************************************************
************************************************************************









