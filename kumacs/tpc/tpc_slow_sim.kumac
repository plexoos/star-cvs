************************************************************************
************************************************************************
macro tpc_slow_sim__
          INPUT_DATA_DIR=$AFS_RHIC/star/data/samples/ _
          input_g2t_file=gstar_ven_phys_on_auau_2central.xdr_
          num_events=1_
          tpc_sector_first=18_
          tpc_sector_last=18_
          magfield=0.5_
          funit=21 _
          nfile=tphit.ntup _
          nid=111_
          table=/dui/data/tpc/hits/tphit


*  
* --> to run
*    - change the inputs above as desired
*    - run staf++
*    -   make tpc   (you'll get pams needed: tpg,tfc,tss,tcl)
*    -   exec tpc_slow_sim
*         
* in general, the things in [small letters] are input values
*                           [CAPITAL LETTERS] are directories   
*
*

message ' *** Data sample directory -->  ' [INPUT_DATA_DIR]
message ' *** Input G2T file -->         ' [input_g2t_file]
message ' *** Num Events to process -->  ' [num_events]
message ' *** TPC Sector first,last -->  ' [tpc_sector_first],[tpc_sector_last]
message ' *** Magnetic Field -->         ' [magfield]
message ' *** ntup file unit -->         ' [funit]
message ' *** output Ntuple filename  -->' [nfile]
message ' *** Ntuple ID # -->            ' [nid]
message ' *** Table to write to ntup --> ' [table]
 
* global macros
*  exec $STAR/kumacs/util/setup_global_macros
  exec /scr22/kathy/test98b_tfs/kumacs/util/setup_global_macros
  macro/global/import _
         KUMACS_UTIL,PAMS_TPC_KUMAC,DATA_SAMPLES,KUMACS_TPC,PARAMS_TPC 

* generic inits
  exec [KUMACS_UTIL]setup_dir
  exec [KUMACS_UTIL]input_g2t_file#init [INPUT_DATA_DIR][input_g2t_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA

* tpc geometry init
  exec [PAMS_TPC_KUMAC]tpg_init#init

* tpc slow sim init
  exec initialize [tpc_sector_first] [tpc_sector_last]  [magfield] 

* init to write table from tpc fast sim to ntuple
   exec [KUMACS_UTIL]write_table_to_ntuple#init_disk _
       [funit] [nfile] [nid] [table]

* read event and then process thru tpc slow sim
  do i=1,[num_events]
    message ' processing event ' [i]
    exec [KUMACS_UTIL]input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA    
    exec process_event [i] 
* write table from tpc fast sim to ntuple
    if [num_events]=1 then
      exec [KUMACS_UTIL]write_table_to_ntuple#write_event_disk [nid] [table]
    endif
  enddo

  ENDOFDATA:
    if [i].lt.[num_events] then
     message 'End of input file'
    endif

* close ntuple
  message 'close ntuple'
  exec [KUMACS_UTIL]write_table_to_ntuple#close_disk [funit]

* close input file
  message 'close input file'
  exec [KUMACS_UTIL]input_g2t_file#close

return

************************************************************************
************************************************************************
************************************************************************
************************************************************************
************************************************************************
*--------------------------------------------------------------------

macro initialize_
               tpc_sector_first_ 
               tpc_sector_last_ 
               magfield

  message ' Initializing for tpc slow simulator '

  macro/global/import _ 
     KUMACS_UTIL,PAMS_TPC_KUMAC,DATA_SAMPLES,KUMACS_TPC,PARAMS_TPC 


*
*  create tables
    exec [KUMACS_TPC]create_tables_tpc_slow_sim#tss
    exec [KUMACS_TPC]create_tables_tpc_slow_sim#tfc
    exec [KUMACS_TPC]create_tables_tpc_slow_sim#tcl

*   initialization for tpc slow simulator
    exec [PAMS_TPC_KUMAC]tss_init#init _
                      [tpc_sector_first] [tpc_sector_last] [magfield]
    exec [PAMS_TPC_KUMAC]tcl_init#init

 return

*------------------------------------------------------------------------

macro process_event  nev

   macro/global/import _
      KUMACS_UTIL,PAMS_TPC_KUMAC,DATA_SAMPLES,KUMACS_TPC,PARAMS_TPC 

   message ' TPC slow sim: Processing Event # ' [nev]


* call tssam - simulated tpc pixels
  exec [PAMS_TPC_KUMAC]tss_run#run

* call xyz - reordered tpc pixels with more info
  exec [PAMS_TPC_KUMAC]tfc_run#run 

* call tcl_make_clusters - tpc cluster finder
  exec [PAMS_TPC_KUMAC]tcl_run#run_tcl_make_clusters

* call tpham - tpc hit finder
  exec [PAMS_TPC_KUMAC]tcl_run#run_tpham 


return


*********************************************************************
*********************************************************************
*********************************************************************
*********************************************************************









