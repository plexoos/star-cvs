*test.kumac - to analyze the test data (with an input that works at BNL)
* version 10 February 1998 to serve as a standard test macro.- WALove
*======================================================================
*-----------------------------------------------------------------------
macro run
exec init
exec loop 10
graphics/opt stat
zone 2 1
ntuple/plot 200.resz -.5<resz<1
ntuple/plot 200.resxy -.5<resxy<1
exec end
return

macro init_
     checkev='ignore'_ 
     sector='laser'_
     infile=$STAR_DATA/samples/piece_of_t28-f5-las.xdf_
     mapfile=' '_
     outfile=' '_
     outdir=' '_
     online_gains=on_
     res=on_
     ntfile=residuals.nt

trace off
* Create Table Definitions / Set up Switches
exec /afs/rhic/star/starlib/ref/sim/tfc/wrk/setup_fmt [mapfile]
exec strack setup [sector]
cell/putvalue 'Switches/fmtpar[0].ped_subtract' 2
cell/putvalue 'Switches/fmtpar[0].data_size' 2
cell/putvalue 'Switches/fmtpar[0].thresh' 6.0
cell/putvalue 'Switches/fmtpar[0].printf' 0
*initialise plotting setup
exec xyz setup

*initialise residuals calculations
if [res]=on then
   shell rm [ntfile]
   hi/file 2 [ntfile] 1024 N
   nt/create 200 'control ' 19 '//LUN2' 1000 resxy resz alpha lambda _
   row x y z event cluster npad charge npnt chisqxy chisqz_
   xave sigma skew kurto

   cdir //PAWC
*  prepare the join command
*  join the track and the hit table, so the info about the track
*  could be added to the residuals

   newsort s025 id
   newjoin j025 '{tphit.id id, tphit.alpha alpha, tphit.lambda lambda, _
   tphit.row row, tphit.x x, tphit.y y, tphit.z z, tphit.track track, _
   tphit.cluster cluster, _
   tphit.q q, xave, sigma, skew, kurto}' '{tphit.id tphitau.id}'

   newsort s050 track
   newsort s050a trk
   newjoin j050 '{id, alpha, lambda, row, x, y, z, track, cluster, _
   q, xave, sigma, skew, kurto, npnt, chisqxy, chisqz}' _
   '{ntphit.track strack.trk}'

   newsort s100 hit
   newsort s100a id
   newjoin j100 '{resy, resz, alpha, lambda, row, x, y, z, track, cluster,_
   q, xave, sigma, skew, kurto, npnt, chisqxy, chisqz}' _
   '{diff_res.hit newhit.id}'

   newsort s200 cluster
   newsort s200a flag
   newjoin j200 '{resy, resz, alpha, lambda, row, x, y, z, track, _
   cluster, q, xave, sigma, skew, kurto, npnt, chisqxy, chisqz, nseq}' _
   '{hitres.cluster tpcluster.flag}'
endif

* Open input/output data files
message Opening: [infile]
newfile raw_data [infile] r
if staf_status(1).ne.1 then
	message Cannot open input file: [infile]
        wait
endif  

* Skip events until you reach either first event
* or BEGIN_RUN

MOREDATA:
message getting a record from raw_data
stream/get raw_data	| read the BEGIN_RUN event
do j=1,6
rowcount BEGIN_RUN/IT[j]
gotb=staf_status(1)
rowcount TPC_DATA/ST[j]
gota=staf_status(1)
if [gotb]+[gota]>0 then
  goto GOTIT
endif
enddo
goto MOREDATA

GOTIT:
message found BEGIN_RUN record
if [outfile]<>' '.and.[outdir]<>' ' then
  message Opening output file: [outfile]
  newfile out [outfile] w
endif

* Process Data from BEGIN_RUN event

if [online_gains]=on then
  cell/putvalue 'Switches/fmtpar[0].event_type' -3
  do j = 1, 6
    ami/module/call reformat_
	Switches/fmtpar_
	BEGIN_RUN/IT[j]_
	TPC_DATA/BD[j]_
	TPC_DATA/SD[j]_
	BEGIN_RUN/ST[j]_
	BEGIN_RUN/GN[j]_
	Maps/native_map_
	Maps/valid_
	PedestalsGains/pedestal_
	PedestalsGains/pulser_
	PedestalsGains/gain_
	ProducedData/Pixels/tppad_
	ProducedData/Pixels/tppixel
  enddo
endif
message reformatted BEGIN_RUN data
*if there was no on-line gain and no BEGIN_RUN event,
*get gains from a file

rowcount PedestalsGains/gain
if Staf_result(1)=0 then
message 'need to get gain from a file'
wait
newfilestream gain $STAR_DATA/SC97/ana/tpc/systest/tmp_gain.xdf r
dui/cd /dui
stream/get gain
stream/close gain
endif

cell/putvalue 'Switches/fmtpar[0].event_type' 0
cell/putvalue 'Geometry/tpgpar/tpg_detector[0].drift_length' 210.0
message finished init macro
return

macro loop  maxevents=1 strack=on plot=both res=on ntrk=100 laser=onw
oldevent = 0
i=1
nmixed=0
message Reading event [i]...
stream/get raw_data

istat = staf_status(1)
while [istat]=1 .and. [i]<=[maxevents] do

* Allow skipping of some events
  case [i] in
    (-1) message Skipping event [i]...
	goto READIT
    (*) *
  endcase

* Ensure "fresh" tables for each event
  ievent = -1
  do RDO = 1, 6
    rowcount TPC_DATA/IT[RDO]
    row_m1 = -1
    if staf_status(1)=1 then
      row_m1 = staf_result(1) - 1
    endif

    if [row_m1]<0 then ; nextl ; endif
    do j = 0, [row_m1]
      cell/getvalue 'TPC_DATA/IT'//[RDO]//'['//[j]//'].data_type'
      if staf_result(1)=128 then
        cell/getvalue 'TPC_DATA/IT'//[RDO]//'['//[j]//'].data_row'
        newevent = staf_result(1)
        breakl;
      endif
    enddo
*    message IT[RDO]: Event Count = [newevent]
    if [ievent]=-1 then
      ievent = [newevent]
    else
      if [newevent]<>[ievent].and.[checkev]=on then
        message Mixed tables from events [newevent] and [ievent] !!!
	nmixed = [nmixed]+1
	if [nmixed]>3 then
	  exec warning_message
	endif
        goto READIT
      else
	if [nmixed]>0 then
	  exec self_corrected [nmixed]
	  nmixed=0
	endif
      endif
    endif
  enddo
  if [ievent] = [oldevent] then
    message Event Count NOT incremented...
    goto READIT
  endif
  oldevent = [ievent]

* Zero out all of the produced tables
  rowcount ProducedData/Pixels/tppad 0
  rowcount ProducedData/Pixels/tppixel 0
  rowcount ProducedData/Pixels/tpmcpix 0
  rowcount ProducedData/Clusters/tpcluster 0
  rowcount ProducedData/Clusters/tpseq 0
  rowcount ProducedData/Hits/tphit 0
  rowcount ProducedData/Hits/tphitau 0
  rowcount ProducedData/Tracks/res 0
  rowcount ProducedData/Tracks/diff_res 0
  rowcount ProducedData/Tracks/strack 0

* Process this event
  message Calling Reformat for tables 1 thru 6...
  timing off
  do j = 1, 6
    ami/module/call reformat_
	Switches/fmtpar_
	TPC_DATA/IT[j]_
	TPC_DATA/BD[j]_
	TPC_DATA/SD[j]_
	TPC_DATA/ST[j]_
	BEGIN_RUN/GN[j]_
	Maps/native_map_
	Maps/valid_
	PedestalsGains/pedestal_
	PedestalsGains/pulser_
	PedestalsGains/gain_
	ProducedData/Pixels/tppad_
	ProducedData/Pixels/tppixel
  enddo
  timing on

* Perform Cluster/Hit/Track-finding
  if [strack]=on.or.[strack]=both  then 
     exec strack 
     message finished strack
  endif

  if [strack]=off.or.[strack]=both then
    exec xyz unpack
  endif


* Plot it
  if [plot]<>off then

     if [plot]=outer then
       lx=-50
       hx=50
       ly=-200
       hy=-120
     elseif [plot]=inner then
       lx=-30; hx=30; ly=-130; hy=-50;
     elseif [plot]=both then
        lx=-80; hx=80; ly=-200; hy=-50;
     else
       message "invalid 3rd argument in the loop call"
       message "valid plot options are off/both/inner/outer"
       wait
     endif

     if [strack]=on.or.[strack]=both then
        cdir //PAWC
        exec draw_track on [lx] [hx] [ly] [hy] [ievent] [strack] [laser]
     endif

  endif

* Save output
*  if [outfile]<>' '.and.[outdir]<>' ' then
*    stream/put out /dui/[outdir]
*    message saving output
*  endif
* select events with 1 reconstructed track (except for lasers - tests ntrk)
timing off
message counting tracks
  rowcount ProducedData/Tracks/strack
  if Staf_result(1).le.[ntrk] then
* calculate the residuals - this part changes tables, so be careful!
  if [res]=on.and.[strack]<>off then
* join the hit table and the track table
*
* must zero these joined tables or else you get old data in your merged tables
*  be careful indeed! - mal 31aug97
     rowcount ntphit 0
     rowcount newhit 0
     rowcount hitres 0
     rowcount final  0
message emptied the four joined tables
     join j025 ProducedData/Hits/tphit  ProducedData/Hits/tphitau ntphit
     join j050 ntphit  ProducedData/Tracks/strack  newhit
     join j100 ProducedData/Tracks/diff_res newhit hitres
     dui/cd ProducedData/Clusters
     rowcount tpcluster
     do is=1,Staf_Result(1)
       iss=[is]-1
       table/cell/put 'tpcluster['//[iss]//'].flag' [is]
     enddo
     dui/cd /dui
message making join j200
     join j200 hitres ProducedData/Clusters/tpcluster _
          final

     rowcount final
 message copying rows of nt 100 to 200 filtered by final.f
     do is=0,Staf_Result(1)-1
        table/cell/put 'final['//[is]//'].track' [newevent]
     enddo
     soc/object/delete tntCWNtuple100 tntCWNtuple
     newcwnt 100 final
APPLICATION COMIS QUIT
      REAL FUNCTION final(XDUMMY)
*********************************************************
*                                                       *
* This file was generated by HUWFUN.                    *
*                                                       *
*********************************************************
*
*     Ntuple Id:      100
*     Ntuple Title:   final
*     Creation:       10/06/97 09.30.17
*
*********************************************************
*
      LOGICAL         CHAIN
      CHARACTER*128   CFILE
      INTEGER         IDNEVT,NCHEVT,ICHEVT
      REAL            VIDN1,VIDN2,VIDN3,VIDN(10)
*
      COMMON /PAWIDN/ IDNEVT,VIDN1,VIDN2,VIDN3,VIDN
      COMMON /PAWCHN/ CHAIN, NCHEVT, ICHEVT
      COMMON /PAWCHC/ CFILE
      real xx(19)
*
*--   Ntuple Variable Declarations
*
      REAL resy,resz,alpha,lambda,x,y,z,q,chisqxy,chisqz,
     +     xave, sigma, skew, kurto
      INTEGER row,track,cluster,nseq,npnt
*
      COMMON /PAWCR4/ resy,resz,alpha,lambda,row,x,y,z,track,cluster, q
     +, xave, sigma, skew, kurto, npnt, chisqxy, chisqz, nseq
*
      xx(1)=resy
      xx(2)=resz
      xx(3)=alpha
      xx(4)=lambda
      xx(5)=mod(row,100)
      xx(6)=x
      xx(7)=y
      xx(8)=z
      xx(9)=track
      xx(10)=cluster
      xx(11)=nseq
      xx(12)=q
      xx(13)=npnt
      xx(14)=chisqxy
      xx(15)=chisqz
      xx(16)=xave
      xx(17)=sigma
      xx(18)=skew
      xx(19)=kurto

      call hcdir('//LUN2',' ')
      call hfn(200,xx)
      call hcdir('//PAWC',' ')
*--   Enter user code here
*

      final = 1.
*
      END
QUIT
     nt/loop 100 final(1.0)
    message finished loop over nt 100
  endif
  endif
timing on

  READIT:
  i = [i]+1
  if [i]<=[maxevents] then
    message Reading event [i]...
    stream/get raw_data
    istat = staf_status(1)
  endif

endwhile

return

macro skip nevents=0
do i=1,[nevents]
stream/get raw_data
enddo
return

macro end
stream/close raw_data
stream/close out
cdir //LUN2
hrout 200
hrout 86
close 2

return

*-----------------------------------------------------------------------
* run cluster/hit/track finding
*-----------------------------------------------------------------------
macro strack 1=0 sector=both

if [1]=setup then
  message Setting up for tracking...

 message  Read geometry files
  exec /afs/rhic/star/packages/SL98b/kumacs/tpc/tpc_geo_laser.kumac
* exec read_geometry.kumac
 message  set the clock and the inner sector Z offset
tdm/table/cell/putvalue _
   'Geometry/tpgpar/tpg_detector[0].clock_frequency' 9.4345e+6
tdm/table/cell/putvalue _
   'Geometry/tpgpar/tpg_detector[0].z_inner_offset' -0.533
 message  read_geometry leaves out one...
 newtable Geometry/tpgpar/tpg_pad tpg_pad 1
 
 message initialize the geometry package
 table/print  Geometry/tpgpar/tpg_detector
 ami/module/call tpg_main_
        Geometry/tpgpar/tpg_pad_plane_
        Geometry/tpgpar/tpg_detector_
        Geometry/tpgpar/tpg_pad

* Define some Slow-Simulator Switches that are needed by the
* clusterfinder as well
  dui/cd /dui/Switches
  exec /afs/rhic/star/starlib/ref/sim/tss/wrk/tss_pars.kumac
 message get tss_pars
*  exec tss_pars.kumac

* Define some Cluster-Finder Switches
  dui/cd /dui/Switches
  exec /afs/rhic/star/starlib/ref/ana/tcl/wrk/setup_tcl.kumac
 message get tcl pars
*  exec setup_tcl.kumac
  tdm/table/cell/putvalue 'tclpar[0].decon' 1
  dui/cd /dui/Switches
* Define some Straight-Tracking Switches
  exec /afs/rhic/star/starlib/ref/ana/tpt/wrk/sts_pars.kumac
 message get the sts_pars
*  exec sts_pars.kumac
tdm/table/cell/putvalue 'tpt_spars[0].first_row' 1
tdm/table/cell/putvalue 'tpt_spars[0].last_row' 45
tdm/table/cell/putvalue 'tpt_spars[0].nmin' 20
if [sector]= laser then
tdm/table/cell/putvalue 'tpt_spars[0].nmin' 6
tdm/table/cell/putvalue 'tpt_spars[0].hole' 4
tdm/table/cell/putvalue 'tpt_spars[0].outlimit' 4
elseif [sector]= inner then
tdm/table/cell/putvalue 'tpt_spars[0].last_row' 13
tdm/table/cell/putvalue 'tpt_spars[0].nmin' 5
elseif [sector]= outer then
tdm/table/cell/putvalue 'tpt_spars[0].first_row' 14
endif

  dui/cd /dui

  newtable ProducedData/Pixels/tpmcpix tss_tpmcpix 1

  dui/mkdir  ProducedData/Clusters
  newtable ProducedData/Clusters/tpcluster tcl_tpcluster 20000
  newtable ProducedData/Clusters/tpseq tcl_tpseq 20000

  dui/mkdir  ProducedData/Hits
  newtable ProducedData/Hits/tphit tcl_tphit 10000
  newtable ProducedData/Hits/tphitau tcl_tphit_aux 10000

  dui/mkdir  ProducedData/Tracks
  newtable ProducedData/Tracks/res tpt_res 10000
  newtable ProducedData/Tracks/diff_res tpt_res 10000
  newtable ProducedData/Tracks/strack tpt_strack 1000

else

  message Calling Cluster-Finder...
  timing off
  ami/module/call tcl_make_clusters_
	Geometry/tpgpar/tpg_pad_plane_
	ProducedData/Pixels/tppad_
	ProducedData/Pixels/tppixel_
	ProducedData/Pixels/tpmcpix_
	ProducedData/Clusters/tpcluster_
	ProducedData/Clusters/tpseq
  timing on

  message Calling Hit-Finder...
  timing off
  ami/module/call tpham_
	Switches/tclpar_
	Switches/tsspar_
	Geometry/tpgpar/tpg_detector_
	Geometry/tpgpar/tpg_pad_plane_
	ProducedData/Pixels/tppixel_
	ProducedData/Pixels/tpmcpix_
	ProducedData/Clusters/tpseq_
	ProducedData/Clusters/tpcluster_
	ProducedData/Hits/tphit_
	ProducedData/Hits/tphitau
  timing on
*trace on

  message Calling Straight-Tracker...
  timing off

  ami/module/call tpt_sts_
	Switches/tpt_spars_
	ProducedData/Hits/tphit_
	ProducedData/Tracks/res_
	ProducedData/Tracks/diff_res_
	ProducedData/Tracks/strack

  timing on

endif

return

* ----------------------------------------------------------------------
* plot pixels in cartesian coordinates
* ----------------------------------------------------------------------
macro xyz 1=0

if [1]=setup then
  message Setting up XYZ...
  dui/cd /dui
  newtable ProducedData/Pixels/adcxyz tfc_adcxyz 1000000
endif

if [1]=unpack then

  message Calling XYZ...
  ami/module/call xyz_
	Geometry/tpgpar/tpg_pad_plane_
	Geometry/tpgpar/tpg_detector_
	ProducedData/Pixels/tppixel_
	ProducedData/Pixels/tppad_
	ProducedData/Pixels/adcxyz
endif
if [1]=plot then

  set mtyp 24
  set pmci 2
  soc/object/delete tntCWNtuple10 tntCWNtuple
  hi/file 77 temp.nt ! n
  cdir //LUN77
  newcwnt 10 ProducedData/Pixels/adcxyz
  cdir //LUN77
  hrout 10
  f/cl 77
  h/file 76 temp.nt
  cdir //LUN76
  hrin 10

  set mscf .4
  set pmci 6
  nt/plot 10.y%x adc<3 ! ! ! s
  set pmci 3
  nt/plot 10.y%x adc.gt.2.and.adc.le.3 ! ! ! s
  set pmci 4
  nt/plot 10.y%x adc.gt.3.and.adc.le.10 ! ! ! s
  set pmci 5
  nt/plot 10.y%x 20>adc>10 ! ! ! s
  set pmci 2
  nt/plot 10.y%x adc>19 ! ! ! s

  exec draw_sector
  f/cl 76
  shell rm temp.nt
  cdir //PAWC
  set mscf 1.
endif
return
*-----------------------------------------------------------------------
* Plot hits and tracks in the TPC
*-----------------------------------------------------------------------
macro draw_track new=on xmin=-200 xmax=200 ymin=-200 ymax=200 ievent=0 _
      strack=on laser=off
zone

dui/cd /dui
soc/object/delete tntCWNtuple86 tntCWNtuple
h/del 86
wait
tnt/newcwn 86 ProducedData/Hits/tphit
igset mtyp 20

rowcount ProducedData/Tracks/strack
ntracks=staf_result(1)
rowcount ProducedData/Tracks/res
nhitused=staf_result(1)

vec/crea xx(2) r
vec/crea yy(2) r
vec/crea xa(2) r [xmin] [xmax]
vec/crea ya(2) r [ymin] [ymax]


if [laser]=one.or.[laser]=onw then
nslice=7
if [laser]=one then
ve/create zlow(7) r -75.0 -100.0 -115.0 -140.0 -160.0 -180.0 -200.0
ve/create zhigh(7) r -50.0 -75.0 -100.0 -120.0 -145.0 -165.0 -180.0
else
ve/create zlow(7) r  -10.0 20.0 50.0 75.0 110.0 140.0 175.0
ve/create zhigh(7) r 10.0 40.0 70.0 110.0 140.0 175.0 200.0
endif
else
nslice=1
ve/create zlow(1) r -300.0
ve/create zhigh(1) r 300.0
endif

do il=1,[nslice]
if [new]=on then ;  hplot/null [xmin] [xmax] [ymin] [ymax] ; endif

igset plci 4

* Draw the X-Y outline
exec draw_sector
     set txal 23
     itx [xmin]+10 [ymax]-10 'Event '//[ievent]
     itx [xmin]+10 [ymax]-20 'Tracks '//[ntracks]

message Drawing [ntracks] tracks...

set pmci [il]
igset mtyp 20
set mscf .6
zl=zlow([il])
zh=zhigh([il])
nt/pl 86.y%x [zl]<z<[zh] ! ! ! s
do itrack=1,[ntracks]

  itrack_less1=[itrack]-1
  getvalue 'ProducedData/Tracks/strack['//[itrack_less1]//'].bzx'
  bzxc=staf_result(1)
  if [bzxc].gt.zlow([il]).and.[bzxc].lt.zhigh([il])  then
    getvalue 'ProducedData/Tracks/strack['//[itrack_less1]//'].ax'
    ax = staf_result(1)
    getvalue 'ProducedData/Tracks/strack['//[itrack_less1]//'].ay'
    ay = staf_result(1)
    do ipoint=1,2
      xpt=xa([ipoint])
      newy=(1-[ax]*[xpt])/[ay]
      vec/in yy([ipoint]) [newy]
    enddo
    if [ntracks]<9 then
      set plci [ntracks]
    else
      set plci 6
    endif
    line xa(1) yy(1) xa(2) yy(2)
  elseif [bzxc].lt.-900.0  then
    getvalue 'ProducedData/Tracks/strack['//[itrack_less1]//'].bzy'
    if staf_result(1).gt.zlow([il]).and.staf_result(1).lt.zhigh([il])  then
      getvalue 'ProducedData/Tracks/strack['//[itrack_less1]//'].ax'
      ax = staf_result(1)
      getvalue 'ProducedData/Tracks/strack['//[itrack_less1]//'].ay'
      ay = staf_result(1)
      if [ntracks]<9 then
        set plci [ntracks]
      else
        set plci 2
      endif
      do ipoint=1,2
        ypt=ya([ipoint])
        newx=(1-[ay]*[ypt])/[ax]
        vec/in xx([ipoint]) [newx]
      enddo
      line xx(1) ya(1) xx(2) ya(2)
    endif
  endif

  if [ntracks]<9 then
    igset mtyp 4
    set pmci [itrack]
    do ihit=1,[nhitused]
      ihit_less1=[ihit]-1
      getvalue 'ProducedData/Tracks/res['//[ihit_less1]//'].trk'
      trknum = staf_result(1)
      getvalue 'ProducedData/Tracks/res['//[ihit_less1]//'].hit'
      hitnum = staf_result(1)
      if [trknum]=[itrack] then
        nt/pl 86.y%x id=[hitnum] ! ! ! s
      endif
    enddo
  endif

enddo

*if too many tracks
* plot all the hits that were used on tracks
*at once

if [ntracks]>8 then
  igset pmci 4
  zlw=zlow([il])
  zhi=zhigh([il])
  nt/pl 86.y%x track>0.and.[zlw]<z<[zhi] ! ! ! s
endif

atitle x
* wait
enddo
v/del xa,ya,xx,yy

     if [strack]=off.or.[strack]=both then
        exec xyz plot
     endif
*wait
if [ntracks]>0 then
set pmci 1
set plci 2
igset mtyp 20
set mscf .6
nt/plo 86.y%z
text 0.0 -35.0  'Event '//[ievent] 0.4
text 50.0 -35.0  'Tracks '//[ntracks] 0.4

do itrack=1,[ntracks]
  itrack_less1=[itrack]-1
  getvalue 'ProducedData/Tracks/strack['//[itrack_less1]//'].azy'
  if staf_result(1)<>-999.0 then
    ve/create sl(1) r
    ve/input sl(1) staf_result(1)
    ve/create of(1) r
    getvalue 'ProducedData/Tracks/strack['//[itrack_less1]//'].bzy'
    ve/input of(1) staf_result(1)
    ve/create yy(2) r [ymin] [ymax]
    ve/create zz(2) r
    sigma zz=sl(1)*yy+of(1)
    line zz(1) yy(1) zz(2) yy(2)
    v/del zz, xx,yy,sl,of, zz
  endif
enddo

endif

return
macro draw_sector
* -----------------------------------------------------------------
*	draw a sector, then rotate it...
* -----------------------------------------------------------------

vec/crea angle(13) r
do isect=1,13
  ang = [isect]-0.5
  ang = [ang]*0.5236
  vec/in angle([isect]) [ang]
enddo
sigma cosine=cos(angle)
sigma sine=sin(angle)
vec/del angle

* these numbers estimated from drawings...
* inner and outer field cage...
r_inner=55.0
r_outer=192.0
* boundary between inner and outer  sectors...
r_boundary=122.0
*
* lengthen these radii by (1/cos(15)) because we want radial distance of the
*  CORNERS of the dodecahedrons...
cos15=0.9659
r_inner=[r_inner]/[cos15]
r_outer=[r_outer]/[cos15]
r_boundary=[r_boundary]/[cos15]

sigma xout=[r_outer]*sine
sigma yout=[r_outer]*cosine
*
sigma xin=[r_inner]*sine
sigma yin=[r_inner]*cosine
*
sigma xbound=[r_boundary]*sine
sigma ybound=[r_boundary]*cosine
*
pline 13 xout yout
pline 13 xin yin
pline 13 xbound ybound
*
do isect=1,12
  line xin([isect]) yin([isect]) xout([isect]) yout([isect])
enddo


return
macro warning message

  shell ./beep
  message '         index tables in the current event ...'
  do i = 1, 4
    message ' '
  enddo
  message '   OOO  U   U TTTTT     OOO  FFFFF     SSS  Y   Y N   N  CCC'
  message '  O   O U   U   T      O   O F        S   S  Y Y  NN  N C   C'
  message '  O   O U   U   T      O   O FFF      S       Y   N N N C'
  message '  O   O U   U   T      O   O F         SSS    Y   N  NN C'
  message '  O   O U   U   T      O   O F            S   Y   N   N C'
  message '  O   O U   U   T      O   O F        S   S   Y   N   N C   C'
  message '   OOO   UUU    T       OOO  F         SSS    Y   N   N  CCC'
  do i = 1, 6
    message ' '
  enddo

  ans=<CR>
  read ans 'Press "c" to continue running anyway, or <CR> to stop taking data'
  if [ans]=c .or. [ans]=C then
    message ' '
    message 'Resuming data analysis...'
    message ' '
  else
    stopm
  endif
return

macro self_corrected nmixed=3
  shell ./beep
  message 'One (or more) of the index tables in the previous '
  message '//[nmixed]// event(s) WERE out of sync...'
  message ' '
  message '... the problem appears to have corrected itself.'
  do i = 1, 4
    message ' '
  enddo
return

