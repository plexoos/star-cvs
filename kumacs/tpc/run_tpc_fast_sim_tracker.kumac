* created May 27, 1998 - Kathy Turner
*                      - this is now kumac that drives tpc fast simulator 
*                        and then does tracking
*  editted June 4, 1998 Kathy Turner
* ***********************************************************************
************************************************************************

macro run__
          INPUT_DATA_DIR=$AFS_RHIC/star/data/samples/ _
          input_g2t_file=event_0000050.xdf _
          num_events=1 _
          tracker=tpt _
          funit=21 _
          nfile=tpc_tracks.ntup _
          nid=111 _
          table=/dui/data/tpc/tracks/tptrack
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_tpc_fast_sim_tracker
*         
*
*

message ' *** Data Sample directory -->        ' [INPUT_DATA_DIR]
message ' *** Input G2T file -->               ' [input_g2t_file]
message ' *** Num Events to process -->        ' [num_events]
message ' *** tpc tracker  --->               ' [tracker]
message ' *** ntup file unit -->               ' [funit]
message ' *** output Ntuple filename  -->      ' [nfile]
message ' *** Ntuple ID # -->                  ' [nid]
message ' *** Table to write to ntup -->       ' [table]
 
 
* SETUP DIRECTORIES TO FIND KUMACS:
 
 
 DEFAULT = _
./kumacs/util,_
./kumacs/chain,_
./kumacs/tpc,_
./pams/emc/kumac,_
./pams/ftpc/kumac,_
./pams/global/kumac,_
./pams/svt/kumac,_
./pams/tpc/kumac,_
$STAR/kumacs/util,_
$STAR/kumacs/chain,_
$STAR/kumacs/svt,_
$STAR/kumacs/tpc,_
$STAR/pams/ctf/kumac,_
$STAR/pams/emc/kumac,_
$STAR/pams/ftpc/kumac,_
$STAR/pams/gen/kumac,_
$STAR/pams/global/kumac,_
$STAR/pams/mwc/kumac,_
$STAR/pams/svt/kumac,_
$STAR/pams/tpc/kumac,_
$STAR/pams/trg/kumac
 
 def [DEFAULT]
 message 'Define default kumac search path as' def
 
*  LOAD LIBRARIES
 if $vexist(make_done)=0 then
   make tpc 
   vec/cre make_done
 endif
 
 
* ******************** GENERIC INITS ******************************
 
  exec setup_dir
  exec input_g2t_file#init [INPUT_DATA_DIR][input_g2t_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* ********************** TPC GEOMETRY ***************************
* tpc geometry init
  exec tpg_init#init
  
* ****************** TPC CLUSTER INIT ***************************
* tpc cluster init for fast sim tracking!
  exec tpc_clustering#initialize_fast
  
* ****************** TPC FAST SIM INIT ***************************
* tpc fast sim init
  exec tpc_fast_sim#initialize 
  
* ****************** TPC TRACKING INIT **************************
* tpc tracker init
  exec tpc_tracker#initialize
  
* ****************** TPC EVALUATION INIT **************************
* tpc eval init
  exec tpc_evaluation#initialize
  
  
  
* ****************** TPC FAST SIM PROCESSING ********************
* ****************** TPC TRACKER PROCESSING *********************
* ****************** TPC TRACK EVALUATION ***********************
  
* read event and then process thru tpc fast sim & then tracker
  do i=1,[num_events]
    message ' run_tpc_fast_sim_tracker: processing event ' [i]
*  read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
*  tpc fast simulator
    exec tpc_fast_sim#process_event [i]
      if STAF_STATUS(1).eq.0 then
        message ' problem with tfs '
      endif
* tpc tracking
    if [tracker]=tpt then
      exec tpc_tracker#process_event_tpt [i] 
    elseif [tracker]=tte then
      exec tpc_tracker#process_event_tte [i] 
    endif
* tpc track evaluation
    exec tpc_evaluation#process_event [i]
* write table to ntuple
    if [i]=1 then
      exec write_table_to_ntuple#init_disk _
       [funit] [nfile] [nid] [table] 
    else
      exec write_table_to_ntuple#write_event_disk [nid] [table]
    endif
  enddo

  
* *********************** END STUFF **************************
  
  ENDOFDATA:
    if [i].lt.[num_events] then
    message ' End of input file'
  endif  

* close ntuple
  message ' close ntuple'
  exec write_table_to_ntuple#close_disk [funit]

* close input file
  message ' close input file'
  exec input_g2t_file#close

return

************************************************************************
************************************************************************
************************************************************************
************************************************************************
************************************************************************













