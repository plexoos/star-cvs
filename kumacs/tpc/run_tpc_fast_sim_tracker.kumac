* *************************************************************************
* run_tpc_fast_sim_tracker.kumac
* 
* 
* created May 27, 1998 - Kathy Turner
*                      - this is now kumac that drives tpc fast simulator 
*                        and then does tracking
* editted June 4, 1998 Kathy Turner
* editted 6/25/98 Lidia Didenko
* editted 6/25/98 Kathy Turner
* 7/2/98 - K. Turner - code now requires that you load geometry libraries
* 7/3/98 - K. Turner - make to use bfc_util kumacs and include pam kumacs
* 7/11/98 - K. Turner - change default data file to new version
* 7/13/98 - K. Turner - change to use bfc macros
* 
************************************************************************
************************************************************************

 macro run__
          TOP=/scr22/kathy/test11 _
          domain='geometry tpc' _
          INPUT_DATA_DIR=$AFS_RHIC/star/data/samples/ _
          input_g2t_file=auau_central_hijing.xdf _
          num_events=1 _
          tracker=tpt _
          funit=21 _
          nfile=tpc_tracks.ntup _
          nid=111 _
          table=tpc/tracks/tptrack
* 
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   exec run_tpc_fast_sim_tracker
* 
	  
  message ' *** TOP directory to search for kumacs --> ' [TOP]
  message ' *** domains to load  -->                   ' [domain]
  message ' *** Data Sample directory -->              ' [INPUT_DATA_DIR]
  message ' *** Input G2T file -->                     ' [input_g2t_file]
  message ' *** Num Events to process -->              ' [num_events]
  message ' *** tpc tracker  --->                      ' [tracker]
  message ' *** ntup file unit -->                     ' [funit]
  message ' *** output Ntuple filename  -->            ' [nfile]
  message ' *** Ntuple ID # -->                        ' [nid]
  message ' *** Table to write to ntup -->             ' [table]

***********************************************************
* General Setup
***********************************************************
  
 macro/global/create TOP              [TOP]

* setup default directories to search for kumacs in
* setup default table directory structure in STAF
* load libraries [domain]
    
   exec $STAR/kumacs/chain/bfc_util#bfc_defaults 
   
   exec setup#setup
   macro/global/import *
     
   exec bfc_util#bfc_load [domain]

* **************************************************************
* --------- Setup and run tpc fast simulator & tracker pams -------------*
   
* open input g2t data file
  exec input_g2t_file#init [INPUT_DATA_DIR][input_g2t_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile]  
   
* --------------------- initialization ----------------------------*

* tpc geometry init
   exec tpg_init#init
  
* tpc cluster init for fast sim tracking!
    exec create_tables_tpc#tcl_fast
    exec tcl_init#init 
    
* tpc fast sim init
    exec tfs_init#init
    
* tpc tracker init
    exec create_tables_tpc#tpt
    exec tpt_init#init
    exec tid_init#init
   
* tpc evaluation init
    exec create_tables_tpc#tte
    exec tte_init#init

  
* ------------------- process events ------------------------------*

* read event and then process thru tpc fast sim & then tracker
  do i=1,[num_events]
    message ' run_tpc_fast_sim_tracker: processing event ' [i]
* clear all event tables
    tabclear [event]
* read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
* tpc fast simulator 
    exec tfs_run#run
      if STAF_STATUS(1).eq.0 then
        message ' problem with tfs '
      endif
* tpc tracking
    if [tracker]=tpt then
	exec tpt_run#run
	exec tid_run#run_tde_new      
    elseif [tracker]=tte then
	exec tte_run#run_tte_track
	exec tid_run#run_tde_new
    endif
* tpc track evaluation
    exec tte_run#run_tte
* write table to ntuple
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]
  enddo

  
* -------------------- end stuff -----------------------------*
  
  ENDOFDATA:
    if [i].lt.[num_events] then
    message ' End of input file'
  endif  

* close ntuple
  message ' close ntuple'
  exec write_table_to_ntuple#close_disk [funit]

* close input file
  message ' close input file'
  exec input_g2t_file#close

return
 
************************************************************************













