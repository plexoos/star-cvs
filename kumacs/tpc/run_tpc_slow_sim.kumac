* created May 27, 1998 - Kathy Turner
*                      - this is now kumac that drives tpc fast simulator 
*  editted June 4, 1998 - Kathy Turner
*  editted 6/9/98 Kathy Turner
*  editted 6/25/98 Lidia Didenko
*  editted 6/25/98 Kathy Turner
* ***********************************************************************
************************************************************************
macro run__
          INPUT_DATA_DIR=$AFS_RHIC/star/data/samples/ _
          input_g2t_file=event_0000050.xdf_
          num_events=1_
          tpc_sector_first=18_
          tpc_sector_last=18_
          funit=21 _
          nfile=tphit.ntup _
          nid=111_
          table=tpc/hits/tphit_
          adcxyzon=0
  
  
  
*  this is an example!! you must change to fill your needs
*  
* --> to run
*    - change the inputs above as desired
*    - run staf
*    -   make tpc   (you'll get pams needed: tpg,tfc,tss,tcl)
*    -   exec run_tpc_slow_sim
*
*

message ' *** Data sample directory -->  ' [INPUT_DATA_DIR]
message ' *** Input G2T file -->         ' [input_g2t_file]
message ' *** Num Events to process -->  ' [num_events]
message ' *** TPC Sector first,last -->  ' [tpc_sector_first],[tpc_sector_last]
message ' *** ntup file unit -->         ' [funit]
message ' *** output Ntuple filename  -->' [nfile]
message ' *** Ntuple ID # -->            ' [nid]
message ' *** Table to write to ntup --> ' [table]
message ' *** Fill adcxyz table for diag.? 0,1 = no,yes --> ' [adcxyzon]
 

* SETUP DIRECTORIES TO FIND KUMACS:

 
 DEFAULT = _
./kumacs/util,_
./kumacs/chain,_
./kumacs/tpc,_
./pams/emc/kumac,_
./pams/ftpc/kumac,_
./pams/global/kumac,_
./pams/svt/kumac,_
./pams/tpc/kumac,_
$STAR/kumacs/util,_
$STAR/kumacs/chain,_
$STAR/kumacs/svt,_
$STAR/kumacs/tpc,_
$STAR/pams/ctf/kumac,_
$STAR/pams/emc/kumac,_
$STAR/pams/ftpc/kumac,_
$STAR/pams/gen/kumac,_
$STAR/pams/global/kumac,_
$STAR/pams/mwc/kumac,_
$STAR/pams/svt/kumac,_
$STAR/pams/tpc/kumac,_
$STAR/pams/trg/kumac
 
 def [DEFAULT]
 message 'Define default kumac search path as' def
 

*  LOAD LIBRARIES
 
 if $vexist(make_done)=0 then
   make tpc 
   vec/cre make_done
 endif

* generic inits
  exec setup_dir
  macro/global/import *
  exec input_g2t_file#init [INPUT_DATA_DIR][input_g2t_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA

* tpc geometry init
  exec tpg_init#init

* tpc slow sim init
  exec tpc_slow_sim#initialize [adcxyzon] [tpc_sector_first] [tpc_sector_last]

* now overwrite which sector you want to run (in case you don't want default)
   cd [params]/tpc/tsspars
   tdm/table/cell/putvalue 'tsspar[0].min_sect' [tpc_sector_first]
   tdm/table/cell/putvalue 'tsspar[0].max_sect' [tpc_sector_last]
   message 'Minimum and Maximum Sector numbers for tpc slow simulation: '
   tdm/table/cell/getvalue 'tsspar[0].min_sect'
   tdm/table/cell/getvalue 'tsspar[0].max_sect'
   tdm/table/cell/getvalue 'tsspar[0].bfield'
   cd /dui
   
* tpc clustering
   exec tpc_clustering#initialize 
   
* ****** OPEN OUTPUT NTUPLE FILE ***************
   exec write_table_to_ntuple#init_disk [funit] [nfile]
   
   
* read event and then process thru tpc slow sim
  do i=1,[num_events]
    message ' processing event ' [i]
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA    
    exec tpc_slow_sim#process_event _
           [i] [adcxyzon] [tpc_sector_first] [tpc_sector_last]
    exec tpc_clustering#process_event _
      [i] [tpc_sector_first] [tpc_sector_last]    
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]
  enddo

  ENDOFDATA:
    if [i].lt.[num_events] then
     message 'End of input file'
    endif

* close ntuple
  message 'close ntuple'
  exec write_table_to_ntuple#close_disk [funit]

* close input file
  message 'close input file'
  exec input_g2t_file#close

return

************************************************************************
************************************************************************
************************************************************************
************************************************************************
************************************************************************









