* SYNOPSIS: STAF analysis driver for the SVTanalysis
*
* DESCRIPTION:
*    A kumac STAF driver for SVT data
*    analysis. The kumac includes user setable switches to let
*    the user decide what packages are to be used for specific
*    tasks.
* Authors: H. Caines
*
* History:
*          MARCH 18, 98 - created
* June 3, 1998 - editted by Kathy Turner
* 7/11/98 - K. Turner - change default data file to new version
* 7/11/98 - K. Turner - change to use standard setup kumacs
* *************************************************
 
 MACRO run _
        TOP=$STAR _
        domain=' svt ' _
        num_events=1 _   
        INPUT_DATA_DIR=$AFS_RHIC/star/data/samples/ _
	input_g2t_file=event_0000050.xdf _
	svt_simul=srs _
	svt_tracker=sgr _  
        funit=21 _
        nfile=svt_tracks.ntup _
        nid=111 _
        table=svt/hits/scs_spt
   
* ******************************************************
* *****************************************************
   
  message ' *** TOP directory to search for kumacs --> ' [TOP]
  message ' *** domains to load  -->                   ' [domain]
  message ' *** Data Sample directory -->              ' [INPUT_DATA_DIR]
  message ' *** Input G2T file -->                     ' [input_g2t_file]
  message ' *** Num Events to process -->              ' [num_events]
  message ' *** svt simulator -->                      ' [svt_simul]
  message ' *** svt tracker  --->                      ' [svt_tracker]
  message ' *** ntup file unit -->                     ' [funit]
  message ' *** output Ntuple filename  -->            ' [nfile]
  message ' *** Ntuple ID # -->                        ' [nid]
  message ' *** Table to write to ntup -->             ' [table]
 

* ----------- General setup stuff ------------------------*
  macro/global/create TOP              [TOP]
    
* setup default directories to search for kumacs in
* setup default table directory structure in STAF
* load libraries [domain]
    
 exec $STAR/kumacs/chain/bfc_util#bfc_start [domain]
     
 macro/global/import *
      

* --------- Setup and run svt fast simulator & tracker pams -------------*
   
* open input g2t data file
  exec input_g2t_file#init [INPUT_DATA_DIR][input_g2t_file]

  if STAF_STATUS(1).eq.0 goto ENDOFDATA
  
* open output ntuple
  exec write_table_to_ntuple#init_disk [funit] [nfile]  
   
* --------------- SVT initialization ----------------------------

 exec  init_svg
  
 if [svt_simul] = sss then
    exec init_sss
    exec init_ssf
 elseif [svt_simul] = srs then
   exec create_tables_svt#srs
   exec init_srs
 endif
 
 
 if [svt_tracker].eq.stk .or. [svt_tracker].eq.sgr then
   exec create_tables_svt#sgr
   exec create_tables_svt#stk
   exec init_stk
   exec init_sgr
 endif
 

* ---------------- SVT - event processing -----------------------

 do ievent=1,[num_events]
    message ' run_svt_sim_tracker: processing event ' [ievent]
* clear all event tables
  tabclear [event]
* read input file
    exec input_g2t_file#read
    if STAF_STATUS(1).eq.0 goto ENDOFDATA
   message 'cpu time is ' $cptime
* SVT detector response simulation
    if [svt_simul]= sss then
      exec svt_slow_sim#process_event
      if (staf_status(1) .ne. 1) goto NEXT_EVENT
    elseif [svt_simul]= srs then
      exec svt_fast_sim#process_event
      if (staf_status(1) .ne. 1) goto NEXT_EVENT
    else
      message ' SVT simulator not valid!!'
    endif
* SVT track finding and momentum reconstruction -----------------------
    if [svt_tracker]= stk then
      exec svt_tracker#process_event_stk 
        if (staf_status(1) .ne. 1) goto NEXT_EVENT
    elseif [svt_tracker]=sgr then
      exec svt_tracker#process_event_sgr
        if (staf_status(1) .ne. 1) goto NEXT_EVENT
    else
      message ' svt_tracker not valid! '
    endif
* write table to ntuple
    exec write_table_to_ntuple#write_event_disk [nid] [data]/[table]    
    
  NEXT_EVENT:
   
 enddo
 
* -------------------- end stuff -----------------------------*
  
 ENDOFDATA:

* close ntuple
  message ' close ntuple'
  exec write_table_to_ntuple#close_disk [funit]

* close input file
  message ' close input file'
  exec input_g2t_file#close

RETURN
 
* **************************************************************
