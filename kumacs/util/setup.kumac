* editted 7/9/98 Kathy Turner - don't create subdirectores for GSTAR
* editted 7/10/98 K. Turner - change geant macro definitions to geant_Run 
*                             and geant_Event
* 
**************************************************
 
macro setup 

  macro/global/import *
    
 if $vexist(setup_done)=0 then
   exec setup#dir
   vec/cre setup_done
 endif
 
return
 
**************************************************
 
macro dir
  
  macro/global/import *
   
 MESSAGE ********************************
 MESSAGE *** Setting up directory tree **
 MESSAGE ********************************
 
*************** create global macros *********************************
 
 message ' new version'
 macro/global/create run       /dui/run
 macro/global/create event     /dui/event
 macro/global/create calib     /dui/calib
     
 macro/global/create constants [run]
 macro/global/create params    [constants]/params
 macro/global/create geometry  [constants]/params
 macro/global/create geantR    [run]/geant    
 macro/global/create geant_Run [geantR]/Run
   
 macro/global/create evgen       [event]/evgen    
 macro/global/create geantE      [event]/geant    
 macro/global/create geant_Event [geantE]/Event
 macro/global/create raw_data    [event]/raw_data 
 macro/global/create data        [event]/data
 macro/global/create dst         [event]/dst
   
***************** make directories *************************************
   
* top level directories--------------------------
 mkdir  [run]
 mkdir  [calib]
 mkdir  [event]
 
* constants tables ----------------------------------
 mkdir  [geantR]
 mkdir  [geant_Run]
 mkdir  [params]
  for sys in ctf emc ftpc global mwc svt tpc
    mkdir [params]/[sys]
    mkdir [calib]/[sys]
  endfor
 mkdir  [params]/global/magnetic_field
 
 
*  event generator tables ----------------------------
 mkdir  [evgen]
 
* geant event tables ---------------------------------
 mkdir [geantE]
 mkdir [geant_Event]

*  raw data tables ------------------------------------
 mkdir  [raw_data] 
  for sys in emc ftpc svt tpc
   mkdir  [raw_data]/[sys]
   mkdir  [raw_data]/[sys]/pixels
 endfor
 
*  data tables ----------------------------------------
 mkdir  [data] 
 mkdir  [data]/ctf
 mkdir  [data]/trg       
 mkdir  [data]/mwc
 for sys in emc ftpc global svt tpc
  mkdir  [data]/[sys]
  mkdir  [data]/[sys]/tracks
  if [sys]=global then
    mkdir  [data]/global/vertices
  else
    mkdir  [data]/[sys]/hits
  endif
 endfor
 
*  dst information ---------------------------------------
 mkdir  [dst]
 
 
return
 
******************************************************
 

