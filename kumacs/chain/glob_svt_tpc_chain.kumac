**************************************************
MACRO run_all no_events=1 file=/afs/rhic/star/data/samples/event_0000050.xdf svt_simul=srs svt_tracker=sgr  tpc_simul=tfs tpc_tracker=tpt tpc_svt_match=svm event_refit=egr ev0_finder=ev0
 **************************************************
 * MACRO RUN_ALL
 *
 * SYNOPSIS: STAF analysis driver for the SVT + TPC analysis
 *
 * DESCRIPTION:
 *    A kumac STAF driver for SVT+TPC data
 *    analysis. The kumac includes user setable switches to let
 *    the user decide what packages are to be used for specific
 *    tasks.
 *    The user should set the LOCAL macro to his/her work area.
 *    ANA and SIM point to the reference code and kumacs. The
 *    use of LOCAL rather than ANA or SIM depend on the user's
 *    specific needs.
 *

 * Authors: H. Caines, C. Pruneau
 *
 * History:
 *          Feb 24, 97 - created
 *************************************************
 no_events=[1]
 file = [2]
 LOCAL = $HOME
 PAMS   = /afs/rhic/star/packages/SL98b/pams

 UTIL_HOME = /afs/rhic/star/packages/SL98b/kumacs/util
 KUM_HOME = /afs/rhic/star/packages/SL98b/kumacs/chain
 SVT_HOME = [PAMS]/svt/kumac
 TPC_HOME = [PAMS]/tpc/kumac
 GLOBAL_HOME = [PAMS]/global/kumac
 
 SVG_HOME = [SVT_HOME]
 SSS_HOME = [SVT_HOME]
 SSF_HOME = [SVT_HOME]
 SRS_HOME = [SVT_HOME]
 SGR_HOME = [SVT_HOME]
 STK_HOME = [SVT_HOME]
 SVM_HOME = [GLOBAL_HOME]
 TPG_HOME = [TPC_HOME]
 TFS_HOME = [TPC_HOME]
 TPT_HOME = [TPC_HOME]
 TTE_HOME = [TPC_HOME]
 EGR_HOME = [GLOBAL_HOME]
 EV0_HOME = [GLOBAL_HOME]


 make svt tpc global
 *****************************************
 * variables
 *****************************************
 alias/create STAFCV_OK  1
 alias/create STAFCV_BAD 0

*****************************************
* Create timing vector
*****************************************

v/create vsrs
v/create vsgr
v/create vstk
v/create vtfs
v/create vtpt
v/create vsvm
v/create vegr
v/create vev0

 *****************************************
 * create table directories
 *****************************************
 *****************************************
  
 exec [UTIL_HOME]/setup_dir
 exec [UTIL_HOME]/setup_global_macros

Message 'Doing' [no_events] 'events'
 ***************************************** 
 * open input file and read header
 *****************************************
 exec [UTIL_HOME]/input_g2t_file#init [file]

 *****************************************
 * one time package initialization phase for geometry
 *****************************************
 cd /dui/data/svt

 exec  [SVG_HOME]/init_svg
 if [svt_simul] = sss then
  message 'run_all-I1 : will use svt simulator sss/ssf'
  exec [SSS_HOME]/init_sss
  exec [SSF_HOME]/init_ssf
 endif

 *****************************************
 * TPC geometry
 *****************************************

 exec [TPG_HOME]/tpg_init

 dio/newf output /star/mds/users/ev0out_tte.dsl w

 cd /dui/geant
 stream/getevent G2Tfile
 ls /dui/geant/Event
 *****************************************
 * event loop
 *****************************************

 do ievent=1,[no_events]
    
    if (staf_status(1).ne.STAFCV_OK) goto EOF
    message 'run_all-I3 : doing loop' [ievent]

   

 *  SVT detector response simulation 
   cd /dui/data/svt
$cptime
    if [svt_simul]= sss then
      exec [SSS_HOME]/run_sss [ievent] [SSS_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
      exec [SSF_HOME]/run_ssf [ievent] [SSF_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
 *    exec [SCL_HOME]/run_scl [ievent] [SCL_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    elseif [svt_simul]= srs then
      exec [SRS_HOME]/run_srs  [ievent] [SRS_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    endif
sigma vsrs = vsrs + $cptime


    cd ../svt
 *  svt track finding and momentum reconstruction
    if [svt_tracker]= stk then
      exec [STK_HOME]/run_stk  [ievent] [STK_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto TPC
    elseif [svt_tracker]=sgr then
      exec [SGR_HOME]/run_sgr  [ievent] [SGR_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto TPC
sigma vsgr = vsgr + $cptime
     exec [STK_HOME]/run_stk  [ievent]+1 [STK_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto TPC
    endif
sigma vstk = vstk + $cptime


 *  tpc track reconstruction tpt_run runs real track reconstruction
 *                           tte_run runs ideal track reconstruction
 TPC:

    cd /dui/data/tpc

    if [tpc_simul]=tfs then
      exec [TFS_HOME]/tfs_run [ievent] [TFS_HOME] 
      if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    endif  
sigma vtfs = vtfs + $cptime

    if [tpc_tracker]=tpt then
       exec [TPT_HOME]/tpt_run [ievent] [TPT_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    elseif [tpc_tracker]=tte then
       exec [TTE_HOME]/tte_run [ievent] [TTE_HOME]
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    endif

sigma vtpt = vtpt + $cptime
 *  tpc to svt matching

    cd /dui/data/global

   if [tpc_svt_match]=svm then
      exec [SVM_HOME]/run_svm [ievent] [SVM_HOME]
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   endif
sigma vsvm = vsvm + $cptime
*  event global refit

   cd /dui/data/global
   if [event_refit]=egr then
      exec [EGR_HOME]/run_egr [ievent] [EGR_HOME]
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   endif
sigma vegr = vegr + $cptime
*  V0 find
   cd /dui/data/global
   if [ev0_finder]=ev0 then
      exec [EV0_HOME]/run_ev0  [ievent] [EV0_HOME]
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   endif
sigma vev0 = vev0 + $cptime
* Plots and evaluation

exec [EV0_HOME]/run_ev0#eval  [ievent] [EV0_HOME]

v/print vsrs
v/print vsgr
v/print vstk
v/print vtfs
v/print vtpt
v/print vsvm
v/print vegr
v/print vev0
 ls /dui/data/global

 dio/stream/putevent output /dui

NEXT_EVENT:
 
 dio/stream/getevent G2Tfile
 
enddo

*  task is completed
EOF:

dio/stream/close data

RETURN
