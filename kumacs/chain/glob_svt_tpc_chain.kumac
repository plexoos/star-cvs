** put in tss
** fix directory pointers for tpc parameters - don't use setup global macros
 **************************************************
 * MACRO RUN_ALL
 *
 * SYNOPSIS: STAF analysis driver for the SVT + TPC analysis
 *
 * DESCRIPTION:
 *    A kumac STAF driver for SVT+TPC data
 *    analysis. The kumac includes user setable switches to let
 *    the user decide what packages are to be used for specific
 *    tasks.
 *    The user should set the LOCAL macro to his/her work area.
 *    ANA and SIM point to the reference code and kumacs. The
 *    use of LOCAL rather than ANA or SIM depend on the user's
 *    specific needs.
 *
 *  tpc track reconstruction tpt_run runs real track reconstruction
 *                           tte_run runs ideal track reconstruction
 *
 *
 * Authors: H. Caines, C. Pruneau
 *
 * History:
 *          Feb 24, 97 - created
 *          April 29, 1998 - editted by Kathy
 *
**************************************************
 
MACRO run_all _
   no_events=1 _ 
   INPUT_DATA_DIR=/star/mds/users/yepes/gst/hijing/au_200_ce/ _
   file=event_0000001.xdf _ 
   svt_simul=srs _
   svt_tracker=sgr _  
   tpc_simul=tfs _
   tpc_tracker=tpt _ 
   tpc_svt_match=svm _ 
   event_refit=egr _
   ev0_finder=ev0 


***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************

message ' *** Data sample directory -->  ' [INPUT_DATA_DIR]
message ' *** Input file -->             ' [file]
message ' *** Num Events to process -->  ' [no_events]
message ' *** SVT simulator         -->  ' [svt_simul]
message ' *** SVT tracker           -->  ' [svt_tracker]
message ' *** TPC simulator         -->  ' [tpc_simul]
message ' *** TPC tracker           -->  ' [tpc_tracker]
message ' *** TPC-SVT track matcher -->  ' [tpc_svt_match]
message ' *** Event Refitter        -->  ' [event_refit]
message ' *** EV0 Finder            -->  ' [ev0_finder]


***********************************
* SETUP DIRECTORIES TO FIND KUMACS:
***********************************

 DEFAULT = _
./kumacs/util,_
./kumacs/chain,_
./pams/global/kumac,_
./pams/svt/kumac,_
./pams/tpc/kumac,_
$STAR/kumacs/util,_
$STAR/kumacs/chain,_
$STAR/kumacs/tpc,_
$STAR/pams/ctf/kumac,_
$STAR/pams/emc/kumac,_
$STAR/pams/ftpc/kumac,_
$STAR/pams/gen/kumac,_
$STAR/pams/global/kumac,_
$STAR/pams/mwc/kumac,_
$STAR/pams/svt/kumac,_
$STAR/pams/tpc/kumac,_
$STAR/pams/trg/kumac

 def [DEFAULT]
 message 'Define default kumac search path as' def
wait
*******************************
* SETUP GLOBAL MACROS
*****************************

* need these because the tpc init kumacs use them
 exec setup_global_macros

 macro/global/import  PARAMS_TPC

* macro/global/import  KUMACS_TPC,PARAMS_TPC,PAMS_TPC_KUMAC
* macro/global/import  KUMACS_SVT,PARAMS_SVT,PAMS_SVT_KUMAC
* macro/global/import  KUMACS_EMC,PARAMS_EMC,PAMS_EMC_KUMAC
* macro/global/import  KUMACS_CTF,PARAMS_CTF,PAMS_CTF_KUMAC
* macro/global/import  KUMACS_MWC,PARAMS_MWC,PAMS_MWC_KUMAC
* macro/global/import  KUMACS_TRG,PARAMS_TRG,PAMS_TRG_KUMAC
* macro/global/import  KUMACS_FTPC,PARAMS_FTPC,PAMS_FTPC_KUMAC
* macro/global/import   KUMACS_GLOBAL,PARAMS_GLOBAL,PAMS_GLOBAL_KUMAC


*   message ' get KUMACS_UTIL  = '      [KUMACS_UTIL]
*   message ' get PAMS_SVT_KUMAC= '     [PAMS_SVT_KUMAC]
*   message ' get PAMS_GLOBAL_KUMAC = ' [PAMS_GLOBAL_KUMAC]

****************************************
*  LOAD LIBRARIES
**************************************** 
 if $vexist(make_done)=0 then
   make svt tpc global  
   vec/cre make_done
 endif
 
*************************
* SETUP LOCAL DIRECTORIES
*************************

*   UTIL_HOME=[KUMACS_UTIL]
*   message ' util home = ',[UTIL_HOME]

*** TAKE OUT
*   KATHY_KUMAC=./kumacs/util/
*   message ' kathy kumac area = ' [KATHY_KUMAC]

*   SVG_HOME=[PAMS_SVT_KUMAC]
*   SSS_HOME=[PAMS_SVT_KUMAC]
*   SSF_HOME=[PAMS_SVT_KUMAC]
*   SRS_HOME=[PAMS_SVT_KUMAC]
*   SGR_HOME=[PAMS_SVT_KUMAC]
*   STK_HOME=[PAMS_SVT_KUMAC]
*   SVM_HOME=[PAMS_GLOBAL_KUMAC]

*   TPC_HOME=[KUMACS_TPC]
*   TPC_PAMS_HOME=[PAMS_TPC_KUMAC]   
*   TPG_HOME = [TPC_PAMS_HOME]
*   TFS_HOME = [TPC_PAMS_HOME] 
*   TPT_HOME = [TPC_PAMS_HOME]
*   TTE_HOME = [TPC_PAMS_HOME]

*   EGR_HOME = [PAMS_GLOBAL_KUMAC]
*   EV0_HOME = [PAMS_GLOBAL_KUMAC]

************************** 
* SETUP TABLE DIRECTORIES
**************************

 exec setup_dir

***************************
* SETUP LOCAL VARIABLES
**************************

 alias/create STAFCV_OK  1
 alias/create STAFCV_BAD 0

*****************************************
* Create timing vector
*****************************************

  v/create vsrs
  v/create vsgr
  v/create vstk
  v/create vtfs
  v/create vtpt
  v/create vsvm
  v/create vegr
  v/create vev0


 **=====================================================================
 **   Here's where main processing begins
 **=====================================================================


 ***************************************** 
 * open input file and read header
 *****************************************
 exec input_g2t_file#init [INPUT_DATA_DIR][file]

 if (STAF_STATUS(1).ne. STAFCV_OK) then
     message 'problem opening input file '
     goto EOF 
 endif

 *****************************************
 * SVT initialization - geometry
 *****************************************

* cd /dui/data/svt
 exec init_svg
 message ' called init_svg'
 if [svt_simul] = sss then
  message 'run_all-I1 : will use svt simulator sss/ssf'
  exec init_sss
  exec init_ssf
 endif

 **************************************
 * open new output file
 **************************************
 exec write_table_to_file#init evout ev0out_tte.dsl

 ****************************************
 * TPC geometry &  pam initialization 
 ****************************************

  exec tpg_init#init
  exec tpc_fast_sim#initialize

 *****************************************
 *  MAIN EVENT LOOP 
 *****************************************

 do ievent=1,[no_events]

    exec input_g2t_file#read

    if (staf_status(1).ne.STAFCV_OK) goto EOF

    message 'run_glob_svt_tpc_chain : doing loop, event #' [ievent]

$cptime
   
 *  SVT detector response simulation -----------------------------
   cd /dui/data/svt
    if [svt_simul]= sss then
      exec run_sss [ievent] 
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
      exec run_ssf [ievent] 
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
 *    exec run_scl [ievent] 
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    elseif [svt_simul]= srs then
      exec run_srs  [ievent]
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    else
      message ' svt simulator not valid!!'
    endif
   sigma vsrs = vsrs + $cptime


 *  svt track finding and momentum reconstruction -----------------------
    cd ../svt
    if [svt_tracker]= stk then
      exec run_stk  [ievent] 
	 if (staf_status(1) .ne. stafcv_ok) goto TPC
    elseif [svt_tracker]=sgr then
      exec run_sgr  [ievent] 
	 if (staf_status(1) .ne. stafcv_ok) goto TPC
      sigma vsgr = vsgr + $cptime
      exec run_stk  [ievent]+1 
	 if (staf_status(1) .ne. stafcv_ok) goto TPC
    else
      message ' svt_tracker not valid! '
    endif
    sigma vstk = vstk + $cptime


TPC:

* TPC simulator ---------------------------------------------------------

  if [tpc_simul]=tfs then
    exec tpc_fast_sim#process_event [ievent]
    if (staf_status(1) .ne. stafcv_ok) then
     message ' problem with tpc_fast_sim_process_event'
     goto NEXT_EVENT
    endif
    sigma vtfs = vtfs + $cptime
  else
    message ' not a valid tpc simulator '
    go to NEXT_EVENT
  endif


* TPC tracking ----------------------------------------------------------

   if [tpc_tracker]=tpt then
     exec tpc_fast_sim#run_tracker_tpt [ievent]
     if (staf_status(1) .ne. stafcv_ok) then
      message ' problem with tpc_fast_sim_run_tracker_tpt'
      goto NEXT_EVENT
     endif
   elseif [tpc_tracker]=tte then
     exec tpc_fast_sim#run_tracker_tte [ievent]
     if (staf_status(1) .ne. stafcv_ok) then
      message ' problem with tpc_fast_sim_run_tracker_tte'
      goto NEXT_EVENT
     endif
   else
     message ' tpc tracker not valid!'
   endif
   sigma vtpt = vtpt + $cptime


* TPC to SVT Matching ----------------------------------------------------

    cd /dui/data/global

   if [tpc_svt_match]=svm then
      exec run_svm#run_svm [ievent] 
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   else
     message ' not doing tpc and svt matching'
   endif
   sigma vsvm = vsvm + $cptime


* Event Global Refit -----------------------------------------------

   cd /dui/data/global
   if [event_refit]=egr then
      exec run_egr#egr [ievent] 
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   else
     message ' not doing global event refitting'
   endif
   sigma vegr = vegr + $cptime


*  V0 Finder -----------------------------------------------------------
   cd /dui/data/global
   if [ev0_finder]=ev0 then
      exec run_ev0#ev0_am  [ievent] 
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   else
     message ' not doing v0 finding'
   endif
   sigma vev0 = vev0 + $cptime


* Plots and evaluation ----------------------------------------------

  exec run_ev0#ev0_eval2  [ievent] 

  v/print vsrs
  v/print vsgr
  v/print vstk
  v/print vtfs
  v/print vtpt
  v/print vsvm
  v/print vegr
  v/print vev0

* Write output file
  exec write_table_to_file#write evout /dui/data/global



 NEXT_EVENT:
  
enddo


 **=====================================================================
 **   Here's where main processing ends 
 **=====================================================================


EOF:

* ran out of data before processed all events asked for:
  if [ievent].lt.[no_events] then
   message ' End of input file'
  endif

* close input file 
  message ' close input file'
  exec input_g2t_file#close
 
* close output file
  message ' close output file'
  exec write_table_to_file#close evout 

RETURN


*************************************************************************
*************************************************************************
*************************************************************************
*************************************************************************
*************************************************************************

