* $Id: bfc_mdc.kumac,v 1.17 1998/08/10 15:51:04 kathy Exp $
* $Log: bfc_mdc.kumac,v $
* Revision 1.17  1998/08/10 15:51:04  kathy
* added comments for fzin
*
* Revision 1.16  1998/08/07 23:44:51  kathy
* made output_file to be used for fzout or evout; removed unnecessary user/input from bfc_util with evgin
*
* Revision 1.15  1998/08/03 20:41:58  kathy
* now use detp geometry instead of bfc_util#bfc_gstar_setup to load settings for gstar
*
* Revision 1.14  1998/08/01 20:34:01  kathy
* separated tid from tpt - now it's a separate variable in chain as it should be
*
* Revision 1.13  1998/07/30 00:26:04  fisyak
* Add pritout
*
* Revision 1.12  1998/07/29 20:53:37  kathy
* corrected for unecessary inputs to macros
*
* Revision 1.11  1998/07/28 22:18:56  kathy
* oops!
*
* Revision 1.10  1998/07/28 21:37:55  kathy
* moved bfc_defaults and bfc_load out of bfc_util and into kumacs/util setup_defaults and load_libraries; changed bfc kumacs to reflect this - also now allow input text file to run gstar off of; fixed up some comments
*
* Revision 1.9  1998/07/22 21:31:23  kathy
* changed all names in chains to be 5 characters or less: evg2tin -> g2tin and ev0_eval2 -> ev0_e
*C
* Revision 1.8  1998/07/22 15:05:43  kathy
* fixed for correct gstar kumacs
*
* Revision 1.7  1998/07/22 15:04:35  kathy
* fixed for correct gstar kumacs
*
* Revision 1.6  1998/07/21 23:35:14  kathy
* changed names of some items in chain, separated gstar and g2t parts, made bfc and bfc_mdc consistent
*
****************************************************
* TO RUN:
*
*  1. stardev (or whichever version you want)
*  2. staf
*  3. exec $STAR/kumacs/chain/bfc_mdc
* 
* if you want to change anything, copy kumac to your area and change:
*  4. input values on macro command line
*  5. default directory list where you pick up kumacs from 
**************************************************
* 
*
* SYNOPSIS: STAF analysis driver for running BFC which has
*   event generation,
*   detector simulation in GSTAR,
*   SVT + TPC + EMC  simulation 
*   and tracking and analysis
*
***************************************************************
*
* History:
*          Feb 24, 97 - created by H. Caines, C. Pruneau
*          May 1, 1998 - editted by Kathy Turner
*          May 5, 1998 - Kathy
*          May 22, 1998 - editted by Kathy Turner 
*                       - broke up tpc into simulation & tracker
*  editted June 3, 1998 by Kathy Turner - new versions of tpc kumacs
*  editted June 3, 1998 by Kathy Turner - new versions of svt kumacs
*  editted 6/10/98 Kathy Turner - new tpc tables in new version of tpc kumac
*  editted 6/12/98 Kathy Turner - put in tpc slow sim - had to add new inputs
*                               to macro (adcxyzon,tpc_sector_first,_last)
*  editted 7/2/98 Kathy Turner - tpc code required geometry libraries now!
*  editted 7/21/98 Kathy Turner - fixed to make consistent with bfc.kumac
*  editted 7/21/98 K. Turner - renamed rgst -> rungst
*                                      rg2t -> rung2t
*                                      g2t -> rdg2t 
* editted 7/28/98 K. Turner - now can read in txt file and run gstar (gtxin)
* editted 8/1/98  K. Turner - separated out tid from tpt as requested
* editted 8/3/98  K. Turner - use detp geometry command for gstar setup
* *****************************************************************************
*               
* -- DICTIONARY OF INPUTS TO BFC.KUMAC --
* TOP        - top level directory to search for kumacs 
*            - change if you copy stuff to your area! 
* tpc_sector_first - first sector # of tpc to simulation using tss
* tpc_sector_last -  last sector # of tpc to simulation using tss
* geomtype   - for gstar: tpc_only, field_only, complete, 
*                         4th_off (for svt 4th layer off)
* phystype   - for gstar:  hadr_on, hadr_off, decay_only
*    *** can put 'help' as a value for geomtype or phystype too ***
* domain     - list of domains to load [domain='tpc svt global]
* chain      - list of modules to run **** HAVE TO BE IN ORDER ****
*            - see more details below
* first      - first event to process (should set to 1)
* no_events  - number of events to process
* input_data_prefix - directory of where to find input file
* input_data_file   - input file name
* output_file       - output file name
* 
* --DOMAINS --
*   geometry
*   sim/gstar
*   sim/g2t
*   tpc
*   svt
*   global
* !! In order to get tpc code, you must load BOTH geometry and tpc 
* e.g. domain='geometry tpc'
* 
* 
* -- CHAINS --
* evgin      - read in event generator file for running gstar
* g2tin      - read G2T xdf file (post gstar)
* evin       - read in simulated-data file (post gstar + more) 
* fzin       - read in fz file (post gstar but haven't run g2t)
* gtxin      - read in txt file for running gstar
* rgst       - run gstar from evgin file
* rg2t       - run g2t from rungst(gstar) stuff
* svg        - SVT geometry initialization
* tpg        - TPC geometry initialization
* srs        - SVT  resolution simulator (fast)
* sss        - SVT slow simulator - not implemented yet
* tss        - TPC slow simulator
* tfc        - unpacks tpc raw data into adcxyz table for diagnostics
* evin       - to read from tpc raw data file that already has results of tss
* tcl        - TPC clustering - slow sim only !
* tcl_e      - TPC cluster evaluation - slow sim only
* tfs        - TPC fast simulator
* sgr        - grouping SVT hits into tracks 
* tpt        - TPC track finding
* tte_t      - TPC tte tracker
* tte_e      - TPC track evaluator
* tid        - TPC dE/dx pid
* svm        - tpc and svt track matching
* egr        - refit global tracks
* ev0        - Strange particle reconstruction
* fzout      - write out fz file from gstar (only works if you run gstar!)
* evout      - write event & run directories into xdf file
*
* 
* -- EXAMPLE CHAINS --
* for #1-5, use domain='geometry tpc svt global' 
*       -> take out ones you don't need
* !! In order to get tpc code, you must load BOTH geometry and tpc 
*     e.g. domain='geometry tpc'
* example input file is:  $AFS_RHIC/star/data/samples/auau_central_hijing.xdf _
* 
* 
* 1. read from geant file, svt fast sim, tpc fast sim,
*                            sgr tracker, tpt tracker, tte track evaluator,
*                            tid particle id,
*                            global tracking, global refitting, v0 finder
*                            & write event out
*      chain=' g2tin svg tpg srs tfs sgr tpt tte_e tid svm egr ev0 evout'_
*
* 2. read from geant file, svt fast sim, tpc slow sim, tpc clustering
*                            sgr tracker, tpt tracker, tte track evaluator
*                            tid particle id,
*                            global tracking, global refitting, v0 finder
*                            & write event out
* chain=' g2tin svg tpg srs tss tcl tcl_e sgr tpt tte_e tid svm egr ev0 evout'_
*
* 3. read from geant file, svt fast sim, sgr tracker & write event out
*      chain=' g2tin svg srs sgr evout'_
* 
* 4. read from geant file, tpc fast sim, tpt tracker, tte track eval,
*                             tid particle id,
*                             & write event out
*      chain=' g2tin tpg tfs tpt tte_e tid evout'
* 
* 5. read from geant file, tpc slow sim, tpc clustering, tpt tracker,
*                             tid particle id, 
*                             tte track evaluator, write event out
*    chain=' g2tin tpg tss tcl tcl_e tpt tte_e tid evout'
* 
* 6. to read in event generator file, run gstar & g2t & write out to xdf file
*     domain=' geometry sim/gstar sim/g2t ' _
*     chain=' evgin rgst rg2t evout'
*      example input files:  
*          /star/mds/data/SD98/auau200/evg/central/hijing/set0001/regular/auau_ce_b0-2_1_200.xdf 
* 
* 7. to read from an xdf file that already has raw data simulated on it
*    and then doing tpc clustering and tracking and write out results:
*    domain=' tpc '
* chain=' evin tpg tcl tpt'
* 
*  8. to read in event text file, run gstar & g2t & write out to xdf file
*     domain=' geometry sim/gstar sim/g2t ' _
*     chain=' gtxin rgst rg2t evout'
*      example input files:  
*          $AFS_RHIC/star/data/samples/muons_central_2.txt
*  
* 
*****************************************************************
*****************************************************************
*****************************************************************
****************************************************************** 
* 
* 
MACRO bfc_mdc _
   TOP=$STAR _
   tpc_sector_first=1 _
   tpc_sector_last=24 _
   geomtype='complete 4th_off' _
   phystype=hadr_on _
   domain='geometry sim/g2t sim/gstar tpc svt global' _
   chain='evgin rgst rg2t svg tpg srs tss '_ 
   first=401 _
   no_events=20 _
   no_in_bunch=200 _
   nsplit=20 _
   prefix_in=/star/mds/data/SD98/auau200/evg/central/hijing/set0001/regular/auau_ce_b0-2 _
   prefix_out=auau_ce_b0-2 
   
 
***********************************************************
* CALCULATE STUFF FROM MACRO INPUTS 
* **********************************************************
  
  ff    = $format([no_in_bunch]*int([first]/[no_in_bunch])+1,i5)
  f     = [ff]
  l     = [f]+[no_in_bunch]-1
  last  = [first]+[no_events]-1

  input_file = [prefix_in]_[f]_[l].xdf
  if [no_in_bunch]>[nsplit] then | split mode
    output_file = ""
  endif
 
***********************************************************
* GLOBAL VARIABLES 
* ********************************************************** 
 macro/global/create input_file       [input_file]        'Input file name'
 macro/global/create output_file      [output_file]       'Output file name'
 macro/global/create tpc_sector_first [tpc_sector_first]  'first sector in TPC'
 macro/global/create tpc_sector_last  [tpc_sector_last]   'last sector in TPC'
 macro/global/create ievent           1                   'Event counter'
 macro/global/create TOP              [TOP]
 macro/global/create geomtype         [geomtype]
 macro/global/create phystype         [phystype]   
 macro/global/create output_fzd_file   ""      'Output fz file'
 macro/global/create output_xdf_file   ""      'Output xdf file' 
* trace off
   
***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************
   
 message ' *** TOP level directory     --> ' [TOP]
 message ' *** Input file              --> ' [input_file]        
 message ' *** First event             --> ' [first]
 message ' *** no_in_bunch             --> ' [no_in_bunch]
 message ' *** Num Events to process   --> ' [no_events]
 message ' *** domains to load         --> ' [domain]
 message ' *** modules to process      --> ' [chain]   
 message ' *** prefix_in               --> ' [prefix_in]
 message ' *** prefix_out              --> ' [prefix_out]
 message ' *** Output file name        --> ' [output_file]
 message ' *** tpc_sector_first        --> ' [tpc_sector_first]
 message ' *** tpc_sector_last         --> ' [tpc_sector_last]
 message ' *** physics type            --> ' [phystype]
 message ' *** geometry type           --> ' [geomtype]   
 
***********************************************************
* BEGINNING STUFF 
* **********************************************************
 
 exec $STAR/kumacs/util/setup_defaults domain=[domain]
   
 exec setup#setup
 
  exec bfc_util#bfc_start _
    domain=[domain] chain=[chain] phystype=[phystype] geomtype=[geomtype]
  
 macro/global/import *
 if $iquest(1)<>0 goto ABEND
  
 exec [TOP]/kumacs/chain/bfc_util#bfc_init chain=[chain]
 
* now skip events
 if $index([chain],evgin)>0 then
* skip input event if any
   do ievent = [f],[first]-1
     exec [TOP]/kumacs/chain/bfc_util#bfc_process chain=evgin
   enddo
 endif 
 
 *****************************************
 *  MAIN EVENT LOOP 
 *****************************************
 message $cptime
 
* now split up output files into reasonable sizes
* must create output file names for each type
  do iev=[first],[last],[nsplit]
   iev1=[iev]
   iev2=[iev]+[nsplit]-1
   if [iev2]>[last] then
     iev2 = [last]
   endif
   i1 = $format([iev1],i4.4)
   i2 = $format([iev2],i4.4)
   output_xdf_file = [prefix_out]_[i1]_[i2].xdf
     exec [TOP]/kumacs/chain/bfc_util#bfc_init chain=evout  
   output_fzd_file = [prefix_out]_[i1]_[i2].fzd
     exec [TOP]/kumacs/chain/bfc_util#bfc_init chain=fzout  
   
* here is the loop over events for this particular output file
   do ievent=[iev1],[iev2]
* clean tables
     tabclear [event]
     exec [TOP]/kumacs/chain/bfc_util#bfc_process [chain]
     if $iquest(1)<>0 goto EOF
     if (STAF_STATUS(1).ne. STAFCV_OK) goto EOF
* didn't put "evout" into chain - do it explicitly here
     exec [TOP]/kumacs/chain/bfc_util#bfc_process chain=evout
   enddo
   
* close this output file
   exec [TOP]/kumacs/chain/bfc_util#bfc_finish chain=evout
   wait
 enddo
 
*****************************************
*  FINISH
* ****************************************   
EOF:
  exec [TOP]/kumacs/chain/bfc_util#bfc_finish [chain]
  goto END
ABEND:
* fatal error
  message "<bfc> Fatal error"
END:
RETURN
* ******************************************************************
* ******************************************************************
* ******************************************************************
* ******************************************************************
* ******************************************************************
 
