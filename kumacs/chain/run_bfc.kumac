* ***************************************************
* 
*               RUN_BFC.KUMAC
* 
****************************************************
* STAF analysis driver with SVT, TPC, GLOBAL, EMC to do:
*   event generation
*   gstar
*   simulation 
*   reconstruction
*   evaluation
*   analysis 
* ******************************************************************
* History:
*          Feb 24, 97 - created Helen Caines, Claude Pruneau
*          May 1, 1998 - editted by Kathy Turner
*          May 5, 1998 - Kathy
*          May 22, 1998 - editted by Kathy Turner 
*                       - broke up tpc into simulation & tracker
*  editted June 3, 1998 by Kathy Turner - new versions of tpc kumacs
*   editted June 3, 1998 by Kathy Turner - new versions of svt kumacs
*  editted 6/10/98 Kathy Turner - new tpc tables in new version of tpc kumac
* editted 6/12/98 Kathy Turner - put in tpc slow sim - had to add new inputs
*                               to macro (adcxyzon,tpc_sector_first,_last)
* editted 6/15/98 Kathy Turner - small changes to make work for tpc slow sim
*                              - added documentation to the kumac
*                              - changed to work for running gstar in kumac
* 
* *****************************************************************
* TO RUN (updated 6/15/98)
*
*  1. stardev (or whichever version you want)
*  2. staf
*  3. exec $STAR/kumacs/chain/run_bfc
* 
* if you want to change anything, copy kumac to your area and change:
*  4. input values on macro command line
*  5. default directory list where you pick up kumacs from 
* 
* 
*  === macro command line inputs  ==>
* no_events  - # events to process
* evgen_on   - NOT IMPLEMENTED - turn event generator on (true or false)
* gstar_on   - turn gstar on (true or false)
* if  gstar_on = true, then checks:
*   gstar_debugger - turn debugger on (off or on) 
*   gstar_input_file - (file input to gstar - extension tells type! - see WWW)
*   gstar_intype=phase - input to gstar (phase or file)
*   gstar_output_file -  (output xdf file name from gstar/g2t)
*   gstar_phystype - (type of physics to run gstar == MUONS for now)
*   gstar_geomtype - (type of geometry to run gstar == MUONS for now)
* input_raw_data_type - where to get input raw data from: file or from
*                       the results of running gstar in the kumac (file,gstar)
* if input raw data from file then
*     INPUT_DATA_DIR - file directory 
*     file  - file name
* svt_simul - svt simulator (srs or sss-NOT IMPLEMENTED or  none)
* tpc_simul - tpc simulator (tfs or tss or none)
* if tpc_simul = tss then 
*   adcxyzon - turn on to create adcxyz table (0,1 = off,on)
*   tpc_sector_first - tpc sector first 
*   tpc_sector_last - tpc sector last
* svt_tracker - svt tracker (stk or sgr or none)  
* tpc_tracker - tpc tracker (tpt or tte or none))
* tpc_evaluator - tpe track evaluator (tte or none) - works only with tfs
* tpc_svt_match - tpc & svt track matching (svm or none)
* event_refit - event global refitting (egr or none)
* ev0_finder - v0 finder (ev0 or none)
* emc_sim_jet - DOES NOT WORK for now (on or none) 
* reco_outfile=bfc_data_global.xdf - output file from reconstruction 
*   reco_outtable=/dui/data/global - output table to file
* 
* 
***********************************************************************
***********************************************************************
*********************************************************************** 
***********************************************************************
***********************************************************************
 
MACRO run_all _
  no_events=1 _
  evgen_on=false _
  gstar_on=false _
    gstar_debugger=on _
    gstar_input_file=test.txt _
    gstar_intype=phase _
    gstar_output_file=muons_central_2.xdf _
    gstar_phystype=muons _
    gstar_geomtype=muons _
 input_raw_data_type=file _
   INPUT_DATA_DIR=$AFS_RHIC/star/data/samples/ _
   file=event_0000050.xdf _
 svt_simul=srs _
 tpc_simul=tfs _
   adcxyzon=0 _
   tpc_sector_first=18 _
   tpc_sector_last=18 _
 svt_tracker=sgr _  
 tpc_tracker=tpt _
 tpc_evaluator=tte _
 tpc_svt_match=svm _ 
 event_refit=egr _
 ev0_finder=ev0 _
 emc_sim_jet=none _
 reco_outfile=bfc_data_global.xdf _
   reco_outtable=/dui/data/global 


***********************************************************
* PRINT OUT INPUT VALUES
* **********************************************************
 
message ' *** Num Events to process              --> ' [no_events]
message ' *** turning event generator on         --> ' [evgen_on]
message ' *** turning gstar on (true or false)   --> ' [gstar_on]
message ' ***   gstar debugger (on or off )      --> ' [gstar_debugger]
message ' ***   gstar input file name            --> ' [gstar_input_file]
message ' ***   gstar input type (file or phase) --> ' [gstar_intype]
message ' ***   gstar output file name           --> ' [gstar_output_file]
message ' ***   gstar physics type               --> ' [gstar_phystype]
message ' ***   gstar geometry type              --> ' [gstar_geomtype]
message ' *** Type of raw data input(gstar or file) --> ' [input_raw_data_type]
message ' ***   Data sample directory               --> ' [INPUT_DATA_DIR]
message ' ***   Input file                          --> ' [file]
message ' *** SVT simulator                         --> ' [svt_simul]
message ' *** TPC simulator                         --> ' [tpc_simul]
message ' ***   for tss, adcxyzon                   --> ' [adcxyzon]
message ' ***   for tss, tpc_sector_first           --> ' [tpc_sector_first]
message ' ***   for tss, tpc_sector_last            --> ' [tpc_sector_last]
message ' *** SVT tracker                           --> ' [svt_tracker]
message ' *** TPC tracker                           --> ' [tpc_tracker]
message ' *** TPC-SVT track matcher                 --> ' [tpc_svt_match]
message ' *** Event Refitter                        --> ' [event_refit]
message ' *** EV0 Finder                            --> ' [ev0_finder]
message ' *** EMC simulation and Jets               --> ' [emc_sim_jet]
message ' *** Output file name                      --> ' [reco_outfile]
message ' ***   Table written to output file        --> ' [reco_outtable]
 
 
***********************************
* SETUP DIRECTORIES TO FIND KUMACS:
***********************************

 DEFAULT = _
./kumacs/util,_
./kumacs/chain,_
./kumacs/emc,_
./kumacs/gen,_
./kumacs/sim,_
./kumacs/svt,_
./kumacs/tpc,_
./pams/emc/kumac,_
./pams/global/kumac,_
./pams/sim/kumac,_
./pams/svt/kumac,_
./pams/tpc/kumac,_
$STAR/kumacs/util,_
$STAR/kumacs/chain,_
$STAR/kumacs/emc,_
$STAR/kumacs/gen,_
$STAR/kumacs/sim,_
$STAR/kumacs/svt,_
$STAR/kumacs/tpc,_
$STAR/pams/ctf/kumac,_
$STAR/pams/emc/kumac,_
$STAR/pams/ftpc/kumac,_
$STAR/pams/gen/kumac,_
$STAR/pams/global/kumac,_
$STAR/pams/mwc/kumac,_
$STAR/pams/sim/kumac,_
$STAR/pams/svt/kumac,_
$STAR/pams/tpc/kumac,_
$STAR/pams/trg/kumac


 def [DEFAULT]
 message 'Define default kumac search path as' def

 
* ********************************
* turn debugger on just for GSTAR!
******************************
 if [gstar_on]=true.and.[gstar_debugger].eq.on then
   debug on
 endif
 
************************** 
* SETUP TABLE DIRECTORIES
**************************

 exec setup_dir
 
* **************************************************************************
* **** GSTAR initialization & Loading *****
* **** must be done AFTER setup_dir *******
* ******* Note: Geant library loading slightly different than other software*
* *************************************************************************
 
 if [gstar_on].eq.true then
   exec gstar#init_phys [gstar_phystype] 
   exec gstar#init_geom [gstar_geomtype]
   exec gstar#load_lib
   if [gstar_debugger].eq.on then
    swit 2 3
    next; dcut cave z 225 10 10 .03 .03
   endif
 endif
 
 

****************************************
*  LOAD LIBRARIES
**************************************** 

 if $vexist(make_done)=0 then
   make svt 
   make tpc 
   make global  
   if [emc_sim_jet].eq.on then
     make emc
   endif
   vec/cre make_done
 endif
 
 
***************************
* SETUP LOCAL VARIABLES
**************************

 alias/create STAFCV_OK  1
 alias/create STAFCV_BAD 0

*****************************************
* Create timing vector
*****************************************

  v/create vsrs
  v/create vsgr
  v/create vstk
  v/create vtfs
  v/create vtpt
  v/create vsvm
  v/create vegr
  v/create vev0
  v/create vems
  v/create vjet

  
 **=====================================================================
 **   Here's where main processing begins
 **=====================================================================


 ***************************************** 
 * open raw data input file and read header
 *****************************************
 
 if [input_raw_data_type].eq.file then
   exec input_g2t_file#init [INPUT_DATA_DIR][file]
 endif
 
 if (STAF_STATUS(1).ne. STAFCV_OK) then
     message 'problem opening input file '
     goto EOF 
 endif
 
* ************ OPEN INPUT FILE for GEANT  *********************************
* open input file - this in in GEANT format!
 if [gstar_on].eq.true then
    if [gstar_intype].eq.file then
      user/input tx [gstar_input_file]
      message ' use input file to run '
    elseif [gstar_intype].eq.phase then
      message ' enter particles to generate by hand '
    else
      message 'invalid input type'
    endif
 endif
  
 **************************************
 * open new output file - for processed data
 **************************************
 
 if [reco_outfile].ne.none then 
   exec write_table_to_file#init evout [reco_outfile]
 endif
 
 **************************************
 * open new output file - for gstar & write out Run table
 **************************************
 
 if [gstar_on]=true then
   exec write_table_to_file#init  goutunit [gstar_output_file]
   exec write_table_to_file#write goutunit [geant]/Run
 endif
 
 *****************************************
 * SVT initialization - geometry
 *****************************************

 exec init_svg

 ****************************************
 * TPC geometry initialization
 ****************************************

  exec tpg_init#init


 *****************************************
 * SVT initialization - simulation
 *****************************************

 if [svt_simul] = sss then
   exec svt_slow_sim#initialization
 endif

 *****************************************
 * TPC initialization - for simulation & clustering
 *****************************************
 
  if [tpc_simul]=tfs then
    exec tpc_clustering#initialize_fast
    exec tpc_fast_sim#initialize
  elseif [tpc_simul]=tss then
    exec tpc_slow_sim#initialize _ 
      [adcxyzon] [tpc_sector_first] [tpc_sector_last]
    exec tpc_clustering#initialize
  else
    message ' not a valid tpc simulator! '
  endif
  
 
 *****************************************
 * TPC initialization - tracker
 *****************************************

 if [tpc_tracker].eq.tpt .or. [tpc_tracker].eq.tte then
    exec tpc_tracker#initialize
  endif
  
 *****************************************
 * TPC initialization - track evaluator
 *****************************************
 
  if [tpc_evaluator]=tte then
   if [tpc_simul]=tfs then
     exec tpc_evaluation#initialize
   elseif [tpc_simul]=tss then
     message ' track evaluation NOT AVAILABLE for tpc slow sim'
   endif
  endif
  
 ****************************************
 * EMC  geometry &  pam initialization 
 ****************************************
 
 if [emc_sim_jet] = on then
  exec init_ems
  exec init_jet
 endif

  
 *****************************************
 *  MAIN EVENT LOOP 
 *****************************************

 do ievent=1,[no_events]
   
   message 'run_bfc : doing loop, event #' [ievent]
   
===============> RUN GSTAR  
   
   if [gstar_on].eq.true then
     
* if inputting values by hand:
     if [gstar_intype].eq.phase then
       exec gstar#input_phase
      endif
 
*  do for debugging:
     if [gstar_debugger].eq.on then 
       next; dcut cave z 225 10 10 .03 .03
     endif
     
* run gstar/g2t - automatically reads next event from input file 
     exec gstar#process_event
     

* write out event information to file 
     exec write_table_to_file#write goutunit [geant]/Event
     
   endif
   
=============> GET RAW_DATA
     
   if [input_raw_data_type].eq.file then
     exec input_g2t_file#read
      message ' read in raw data from file '
   elseif [input_raw_data_type].eq.gstar then
       message ' getting raw data from gstar results '
   endif
    
    if (staf_status(1).ne.STAFCV_OK) goto EOF
    
   message $cptime
   
   
=============> DETECTOR SIMULATION
   
 *  SVT detector response simulation -----------------------------

    if [svt_simul]= sss then
         exec svt_slow_sim#process_event [ievent]
 	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    elseif [svt_simul]= srs then
         exec svt_fast_sim#process_event  [ievent]
	 if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
    else
      message ' SVT simulator not valid!!'
    endif
   sigma vsrs = vsrs + $cptime


* TPC simulator ---------------------------------------------------------

  if [tpc_simul]=tfs then
    exec tpc_fast_sim#process_event [ievent]
    if (staf_status(1) .ne. stafcv_ok) then
      message ' problem with tpc_fast_sim_process_event'
      goto NEXT_EVENT
    endif
    sigma vtfs = vtfs + $cptime
  elseif [tpc_simul]=tss then
    exec tpc_slow_sim#process_event _
        [ievent] [adcxyzon] [tpc_sector_first] [tpc_sector_last]
    if (staf_status(1) .ne. stafcv_ok) then
      message ' problem with tpc_slow_sim_process_event'
      goto NEXT_EVENT
    endif
     exec tpc_clustering#process_event _
       [ievent] [tpc_sector_first] [tpc_sector_last]
    if (staf_status(1) .ne. stafcv_ok) then
      message ' problem with tpc_clustering_process_event'
      goto NEXT_EVENT
    endif   
    sigma vtfs = vtfs + $cptime
  else
    message ' TPC simulator not  valid!! '
    go to NEXT_EVENT
  endif
  
  
    
* EMC simulation software ----------------------------------------
 if [emc_sim_jet] = on then
   exec run_ems_interface
   sigma vems = vems + $cptime
 endif
 
====================> TRACKING 
 
* SVT track finding and momentum reconstruction -----------------------

    if [svt_tracker]= stk then
      exec svt_tracker#process_event_stk [ievent]
	 if (staf_status(1) .ne. stafcv_ok) goto TPC
    elseif [svt_tracker]=sgr then
      exec svt_tracker#process_event_sgr [ievent]
         if (staf_status(1) .ne. stafcv_ok) goto TPC 
    else
      message ' svt_tracker not valid! '
    endif
    sigma vstk = vstk + $cptime


TPC:

* TPC tracking ----------------------------------------------------------

   if [tpc_tracker]=tpt then
     exec tpc_tracker#process_event_tpt [ievent]
     if (staf_status(1) .ne. stafcv_ok) then
      message ' problem with tpc_tracker_tpt'
      goto NEXT_EVENT
     endif
   elseif [tpc_tracker]=tte then
     exec tpc_tracker#process_event_tte [ievent]
     if (staf_status(1) .ne. stafcv_ok) then
      message ' problem with tpc_tracker_tte'
      goto NEXT_EVENT
     endif
   else
     message ' tpc tracker not valid!'
   endif
   sigma vtpt = vtpt + $cptime
   
* TPC track evaluator -----------------------------------------
   
   if [tpc_evaluator]=tte.and.[tpc_simul].eq.tfs then
      exec tpc_evaluation#process_event [ievent]
   endif

* TPC to SVT Matching ----------------------------------------------------

   if [tpc_svt_match]=svm then
      exec run_svm#run_svm [ievent] 
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   else
     message ' not doing tpc and svt matching'
   endif
   sigma vsvm = vsvm + $cptime


  
* Event Global Refit -----------------------------------------------

   if [event_refit]=egr then
      exec run_egr#egr [ievent] 
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   else
     message ' not doing global event refitting'
   endif
   sigma vegr = vegr + $cptime

   
====================> PHYSICS STUFF
   
*  V0 Finder -----------------------------------------------------------

   if [ev0_finder]=ev0 then
      exec run_ev0#ev0_am  [ievent] 
	if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   else
     message ' not doing v0 finding'
   endif
   sigma vev0 = vev0 + $cptime


* Plots and evaluation ----------------------------------------------
* take out for now - doesn't work 5/1/98 
*  exec run_ev0#ev0_eval2  [ievent] 

 
* Jet Finder software ----------------------------------------
  if [emc_sim_jet] = on then
    exec run_jet_egrid
    exec run_jet_erj
    sigma vjet = vjet + $cptime
  endif   
  
=======================> TIMING 
  
* Timing values -------------------------------------------------------

  v/print vsrs
  v/print vsgr
  v/print vstk
  v/print vtfs
  v/print vtpt
  v/print vsvm
  v/print vegr
  v/print vev0
  v/print vems
  v/print vjet

  
======================> OUTPUT
  
* Write output file --------------------------------------------------

  exec write_table_to_file#write evout [reco_outtable]



 NEXT_EVENT:
  
enddo


 **=====================================================================
 **   Here's where main processing ends 
 **=====================================================================


EOF:

* ran out of data before processed all events asked for:
  if [ievent].lt.[no_events] then
    message ' Ran into end of input file'
  endif
  
* close output gstar file
  if [gstar_on].eq.true then
     exec write_table_to_file#close goutunit
   endif
   
* close input file 
  if [input_raw_data_type].eq.file then
    message ' close input raw_data file'
    exec input_g2t_file#close
  endif
 
* close output file
  message ' close output processed file'
  exec write_table_to_file#close evout 

  
  
 RETURN


*************************************************************************
*************************************************************************
*************************************************************************
*************************************************************************
*************************************************************************







