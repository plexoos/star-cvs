****************************************************
* TO RUN:
*
*  1. stardev (or whichever version you want)
*  2. staf
*  3. exec $STAR/kumacs/chain/run_bfc
* 
* if you want to change anything, copy kumac to your area and change:
*  4. input values on macro command line
*
*******************************************************************
*
* SYNOPSIS: STAF analysis driver for running BFC which has
*   event generation,
*   detector simulation in GSTAR,
*   SVT + TPC + EMC  simulation 
*   and tracking and analysis
*
******************************************************************
* History:
*  Feb 24, 97 - created by H. Caines, C. Pruneau
*  editted May 1, 1998 -  Kathy Turner
*  editted May 5, 1998 - Kathy
*  editted May 22, 1998 - editted by Kathy Turner 
*                       - broke up tpc into simulation & tracker
*  editted June 3, 1998 by Kathy Turner - new versions of tpc kumacs
*  editted June 3, 1998 by Kathy Turner - new versions of svt kumacs
*  editted 6/10/98 Kathy Turner - new tpc tables in new version of tpc kumac
*  editted 6/12/98 Kathy Turner - put in tpc slow sim - had to add new inputs
*                               to macro (adcxyzon,tpc_sector_first,_last)
* editted 6/22/98 Yuri Fisyak - split into bfc & bfc_util to make into a 
*                               real chain & 
*                               changed to work with new pam kumacs that
*                               have different directories, new svt pams that
*                               have separate inits and make into a chain
* editted 6/25/98 Kathy Turner - fixing up after Yuri - get all pkgs working
* editted 6/26/98 Kathy Turner - put in dictionary of input values &
*                                example chains
* 
* ******************************************************************************
* DICTIONARY OF INPUTS TO BFC.KUMAC
* 
* TOP        - top level directory to search for kumacs 
*            - change if you copy stuff to your area! 
* adcxyzon = 0,1 to turn off,on generation of adcxyz table from tss
* tpc_sector_first - first sector # of tpc to simulation using tss
* tpc_sector_last -  last sector # of tpc to simulation using tss
* domain     - list of domains to load [domain='tpc svt global]
* chain      - list of modules to run **** HAVE TO BE IN ORDER ****
*            - see more details below
* first      - first event to process (should set to 1)
* no_events  - number of events to process
* input_data_prefix - directory of where to find input file
* input_data_file   - input file name
* output_file       - output file name
* 
* 
*  THESE are modules that go into a chain!
* gethij     - read hijing xdf file
* gst        - run gstar with predefined tables 
* g2t        - read G2T xdf file
* svg        - SVT geometry initialization
* tpg        - TPC geometry initialization
* srs        - SVT  resolution simulator (fast)
* sss        - SVT slow simulator - not implemented yet
* tss        - TPC slow simulator 
* tcl        - TPC clustering
* tfs        - TPC fast simulator
* sgr        - grouping SVT hits into tracks 
* tpt        - TPC track finding
* tte_t      - TPC tte tracker
* tte_e      - TPC track evaluator
* svm        - tpc and svt track matching
* egr        - refit global tracks
* ev0        - Strange particle reconstruction
* evout      - write event directory into xdf file

* 
* EXAMPLE CHAINS:
*   1. read from geant file, svt fast sim, tpc fast sim,
*                            sgr tracker, tpt tracker, tte track evaluator,
*                            global tracking, global refitting, v0 finder
*                            & write event out
*      chain=' g2t svg tpg srs tfs sgr tpt tte_e svm egr ev0 evout'_
*
*   2. read from geant file, svt fast sim, tpc slow sim, tpc clustering
*                            sgr tracker, tpt tracker,
*                            global tracking, global refitting, v0 finder
*                            & write event out
*      chain=' g2t svg tpg srs tss tcl sgr tpt svm egr ev0 evout'_
*
*   3. read from geant file, svt fast sim, sgr tracker & write event out
*      chain=' g2t svg srs sgr evout'_
* 
* 4. read from geant file, tpc fast sim, tpt tracker, tte track eval, 
*                             & write event out
*      chain=' g2t tpg tfs tpt tte_e evout'
* 
* 5. read from geant file, tpc slow sim, tpc clustering, tpt tracker,
*                             & write event out
*      chain=' g2t tpg tss tcl tpt  evout'
*  
*
*****************************************************************
*****************************************************************
*****************************************************************
*****************************************************************
* finally the kumac really starts 
*****************************************************************
*****************************************************************
*****************************************************************
 
MACRO bfc _
   TOP=$STAR _
   adcxyzon=0 _
   tpc_sector_first=18 _
   tpc_sector_last=18 _
   domain='tpc svt global ' _
   chain='g2t svg tpg srs tfs sgr tpt tte_e svm egr ev0  evout'_ 
   first=1 _
   no_events=1 _
   input_data_prefix=$AFS_RHIC/star/data/samples/ _
   input_data_file=event_0000050.xdf _
   output_file=bfc_output.xdf 
  
***********************************************************
* CALCULATE STUFF FROM MACRO INPUTS 
***********************************************************
    
 input_file = [input_data_prefix][input_data_file]
 last = [first]+[no_events]-1
  
***********************************************************
* GLOBAL VARIABLES 
* **********************************************************
 
 macro/global/create input_file  [input_file]   'Input file name'
 macro/global/create output_file [output_file]  'Output file name'
 macro/global/create adcxyzon    [adcxyzon]     'flag to turn on adcxyz table'
 macro/global/create tpc_sector_first [tpc_sector_first] 'first sector in TPC'
 macro/global/create tpc_sector_last  [tpc_sector_last]   'last sector in TPC'
 macro/global/create ievent           1          'Event counter'
 macro/global/create TOP              [TOP]

***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************
   
 message ' *** Input file                    --> ' [input_file]
 message ' *** Num Events to process         --> ' [no_events]
 message ' *** domains to load               --> ' [domain]
 message ' *** modules to process            --> ' [chain]   
 message ' *** Output file name              --> ' [output_file]
 
***********************************************************
* BEGINNING STUFF 
* **********************************************************
 exec [TOP]/kumacs/chain/bfc_util#bfc_start domain=[domain]
 macro/global/import *
   
 if $iquest(1)<>0 goto ABEND

 exec [TOP]/kumacs/chain/bfc_util#bfc_init chain=[chain]

*****************************************
*  MAIN EVENT LOOP 
*****************************************
   
 message $cptime
 do ievent=[first],[last]	| loop over events
   tabclear [event]          | zero all tables
   exec [TOP]/kumacs/chain/bfc_util#bfc_process [chain]
 enddo

*****************************************
*  FINISH
* ****************************************
 
  exec [TOP]/kumacs/chain/bfc_util#bfc_finish [chain]
  goto END
  
ABEND:
 message "<bfc> Fatal error"
 
END:
 
RETURN
 
******************************************************************
****************************************************************** 



