* $Id: bfc.kumac,v 1.25 1998/07/31 21:22:38 kathy Exp $
* $Log: bfc.kumac,v $
* Revision 1.25  1998/07/31 21:22:38  kathy
* now can skip events in bfc.kumac
*
* Revision 1.24  1998/07/29 20:53:36  kathy
* corrected for unecessary inputs to macros
*
* Revision 1.23  1998/07/28 22:18:55  kathy
* oops!
*
* Revision 1.22  1998/07/28 21:37:54  kathy
* moved bfc_defaults and bfc_load out of bfc_util and into kumacs/util setup_defaults and load_libraries; changed bfc kumacs to reflect this - also now allow input text file to run gstar off of; fixed up some comments
*
* Revision 1.21  1998/07/22 21:31:22  kathy
* changed all names in chains to be 5 characters or less: evg2tin -> g2tin and ev0_eval2 -> ev0_e
*
* Revision 1.20  1998/07/22 15:32:51  kathy
*  fixed space in input parameters that caused it not to run
*
* Revision 1.19  1998/07/21 23:35:13  kathy
* changed names of some items in chain, separated gstar and g2t parts, made bfc and bfc_mdc consistent
*
****************************************************
* TO RUN:
*
*  1. stardev (or whichever version you want)
*  2. staf
*  3. exec $STAR/kumacs/chain/bfc
* 
* if you want to change anything, copy kumac to your area and change:
*  4. input values on macro command line
*  5. default directory list where you pick up kumacs from 
*******************************************************************
*
* SYNOPSIS: STAF analysis driver for running BFC which has
*   event generation,
*   detector simulation in GSTAR,
*   SVT + TPC + EMC  simulation 
*   and tracking and analysis
*
******************************************************************
*
* History:
*  Feb 24, 97 - created by H. Caines, C. Pruneau
*  editted May 1, 1998 -  Kathy Turner
*  editted May 5, 1998 - Kathy
*  editted May 22, 1998 - editted by Kathy Turner 
*                       - broke up tpc into simulation & tracker
*  editted June 3, 1998 by Kathy Turner - new versions of tpc kumacs
*  editted June 3, 1998 by Kathy Turner - new versions of svt kumacs
*  editted 6/10/98 Kathy Turner - new tpc tables in new version of tpc kumac
*  editted 6/12/98 Kathy Turner - put in tpc slow sim - had to add new inputs
*                               to macro (adcxyzon,tpc_sector_first,_last)
* editted 6/22/98 Yuri Fisyak - split into bfc & bfc_util to make into a 
*                               real chain & 
*                               changed to work with new pam kumacs that
*                               have different directories, new svt pams that
*                               have separate inits and make into a chain
* editted 6/25/98 Kathy Turner - fixing up after Yuri - get all pkgs working
* editted 6/26/98 Kathy Turner - put in dictionary of input values &
*                                example chains
* editted 7/2/98 Kathy Turner - tpc code now required geometry libraries!
* editted 7/9/98 Kathy Turner - track evaluation now available for tpc slowsim
* editted 7/11/98 K. Turner - no longer need input value adcxyzon - use tfc
*                             in chain instead
* editted 7/11/98 K. Turner - added evgin,rgst,rg2t
* editted 7/11/98 K. Turner - added evin
* editted 7/21/98 K. Turner - renamed rgst -> rungst
*                                     rg2t -> rung2t
*                                      g2t -> rdg2t 
* editted 7/28/98 K. Turner - now can read in txt file and run gstar (gtxin)
* editted 7/31/98 K. Turner - now can skip events at beginning (g2tin)
* ******************************************************************************
* -- DICTIONARY OF INPUTS TO BFC.KUMAC --
* TOP        - top level directory to search for kumacs 
*            - change if you copy stuff to your area! 
* tpc_sector_first - first sector # of tpc to simulation using tss
* tpc_sector_last -  last sector # of tpc to simulation using tss
* geomtype   - for running gstar (all, tpc_field,tpc_nofield) 
* phystype   - for running gstar (on,off)
* domain     - list of domains to load [domain='tpc svt global]
* chain      - list of modules to run **** HAVE TO BE IN ORDER ****
*            - see more details below
* first      - first event to process (should set to 1)
* no_events  - number of events to process
* input_data_prefix - directory of where to find input file
* input_data_file   - input file name
* output_file       - output file name
* 
* --DOMAINS --
*   geometry
*   sim/gstar
*   sim/g2t
*   tpc
*   svt
*   global
* !! In order to get tpc code, you must load BOTH geometry and tpc 
*     e.g. domain='geometry tpc'
* 
* 
* -- CHAINS --
* evgin      - read in event generator file for running gstar
* g2tin      - read G2T xdf file
* evin       - read in simulated-data file 
* fzin       - read in fz file
* gtxin      - read in txt file for running gstar
* rgst       - run gstar from evgin file
* rg2t       - run g2t from rungst(gstar) stuff
* svg        - SVT geometry initialization
* tpg        - TPC geometry initialization
* srs        - SVT  resolution simulator (fast)
* sss        - SVT slow simulator - not implemented yet
* tss        - TPC slow simulator
* tfc        - unpacks tpc raw data into adcxyz table for diagnostics
* evin       - to read from tpc raw data file that already has results of tss
* tcl        - TPC clustering - slow sim only !
* tcl_e      - TPC cluster evaluation - slow sim only
* tfs        - TPC fast simulator
* sgr        - grouping SVT hits into tracks 
* tpt        - TPC track finding
* tte_t      - TPC tte tracker
* tte_e      - TPC track evaluator
* svm        - tpc and svt track matching
* egr        - refit global tracks
* ev0        - Strange particle reconstruction
* fzout      - write out fz file from gstar
* evout      - write event & run directories into xdf file
*
* 
* -- EXAMPLE CHAINS --
* for #1-5, use domain='geometry tpc svt global' 
* -> take out ones you don't need
* !! In order to get tpc code, you must load BOTH geometry and tpc 
*     e.g. domain='geometry tpc'
* example input file is:  $AFS_RHIC/star/data/samples/auau_central_hijing.xdf _
* 
* 
* 1. read from geant file, svt fast sim, tpc fast sim,
*                            sgr tracker, tpt tracker, tte track evaluator,
*                            global tracking, global refitting, v0 finder
*                            & write event out
*      chain=' g2tin svg tpg srs tfs sgr tpt tte_e svm egr ev0 evout'_
*
* 2. read from geant file, svt fast sim, tpc slow sim, tpc clustering
*                            sgr tracker, tpt tracker, tte track evaluator
*                            global tracking, global refitting, v0 finder
*                            & write event out
*    chain=' g2tin svg tpg srs tss tcl tcl_e sgr tpt tte_e svm egr ev0 evout'_
*
* 3. read from geant file, svt fast sim, sgr tracker & write event out
*      chain=' g2tin svg srs sgr evout'_
*
* 4. read from geant file, tpc fast sim, tpt tracker, tte track eval, 
*                             & write event out
*      chain=' g2tin tpg tfs tpt tte_e evout'
* 
* 5. read from geant file, tpc slow sim, tpc clustering, tpt tracker,
*                             tte track evaluator, write event out
*    chain=' g2tin tpg tss tcl tcl_e tpt tte_e evout'
* 
* 6. to read in event generator file, run gstar & g2t & write out to xdf file
*     domain=' geometry sim/gstar sim/g2t ' _
*     chain=' evgin rgst rg2t evout'
*      example input files:  
*          /star/mds/data/SD98/auau200/evg/central/hijing/set0001/regular/auau_ce_b0-2_1_200.xdf 
* 
* 7. to read from an xdf file that was produced from bfc.kumac 
*    and then doing tpc clustering and tracking and write out results:
*    domain=' tpc '
* chain=' evin tpg tcl tpt'
* 
*  8. to read in event text file, run gstar & g2t & write out to xdf file
*     domain=' geometry sim/gstar sim/g2t ' _
*     chain=' gtxin rgst rg2t evout'
*      example input files:  
*          $AFS_RHIC/star/data/samples/muons_central_2.txt
*  
*****************************************************************
*****************************************************************
*****************************************************************
*****************************************************************
* finally the kumac really starts 
*****************************************************************
*****************************************************************
*****************************************************************
 
MACRO bfc _
   TOP=$STAR _
   tpc_sector_first=1 _
   tpc_sector_last=1 _
   geomtype=all _
   phystype=on _
   domain=' geometry tpc svt global ' _
   chain='  g2tin svg tpg srs tfs sgr tpt tte_e svm egr ev0 evout' _
   first=1 _
   no_events=1 _
   input_data_prefix=$AFS_RHIC/star/data/samples/ _
   input_data_file=auau_central_hijing.xdf _
   output_file=bfc_output.xdf   
    
***********************************************************
* CALCULATE STUFF FROM MACRO INPUTS 
***********************************************************
    
 input_file = [input_data_prefix][input_data_file]
 last = [first]+[no_events]-1
 
***********************************************************
* PRINT OUT INPUT VALUES
***********************************************************
   
 message ' *** TOP level directory     --> ' [TOP]
 message ' *** Input file              --> ' [input_file]        
 message ' *** Num Events to process   --> ' [no_events]
 message ' *** domains to load         --> ' [domain]
 message ' *** modules to process      --> ' [chain]   
 message ' *** Output file name        --> ' [output_file]
 message ' *** tpc_sector_first        --> ' [tpc_sector_first]
 message ' *** tpc_sector_last         --> ' [tpc_sector_last]
 
***********************************************************
* GLOBAL VARIABLES 
* **********************************************************
 
 macro/global/create input_file  [input_file]   'Input file name'
 macro/global/create output_file [output_file]  'Output file name'
 macro/global/create tpc_sector_first [tpc_sector_first] 'first sector in TPC'
 macro/global/create tpc_sector_last  [tpc_sector_last]   'last sector in TPC'
 macro/global/create ievent           1          'Event counter'
 macro/global/create TOP              [TOP]
 macro/global/create geomtype    [geomtype]
 macro/global/create phystype    [phystype]

***********************************************************
* BEGINNING STUFF 
* **********************************************************
   
 exec $STAR/kumacs/util/setup_defaults 
   
 exec setup#setup
      
 exec bfc_util#bfc_start domain=[domain] chain=[chain] 
   
 macro/global/import *
   
 if $iquest(1)<>0 goto ABEND
 exec bfc_util#bfc_init chain=[chain]
 
***********************************************************
* SKIP EVENTS 
* **********************************************************

   if [first].gt.1 then
     test=[chain]
     nw=$words([test])
     do i=1,[nw]
       mod=$word([test],[i],1)
       if [mod].eq.evgin.or.[mod].eq.g2tin.or.[mod].eq.evin  then 
           message ' going to skip events for ' [mod]
	   do ievent = 1,[first]-1
	       message 'skip event # ',[ievent]
               exec [TOP]/kumacs/chain/bfc_util#bfc_process chain=[mod]
           enddo
       endif
     enddo
   endif

*****************************************
*  MAIN EVENT LOOP 
***************************************** 
 
 message $cptime
 do ievent=[first],[last]	| loop over events
   tabclear [event]          | zero all tables
   exec bfc_util#bfc_process [chain]
 enddo

*****************************************
*  FINISH
* ****************************************
 
  exec bfc_util#bfc_finish [chain]
  goto END
  
ABEND:
 message "<bfc> Fatal error"
 
END:
 
RETURN
 
******************************************************************
****************************************************************** 










