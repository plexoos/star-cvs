* editted 7/2/98 K. Turner - tpc code now requires geometry so make it default
*                            in domain
* ************************************************************
*                     BFC_UTIL.KUMAC
*************************************************************
******************************************************************
macro bfc_start domain='geometry tpc svt global'
  
* SETUP DEFAULT DIRECTORIES TO GET KUMACS FROM
  exec bfc_defaults
  
* SETUP TABLE DIRECTORIES
  exec setup#setup

* SETUP LOCAL VARIABLES & IMPORT MACROS
 alias/create STAFCV_OK  1
 alias/create STAFCV_BAD 0
 macro/global/import *
   
*  LOAD LIBRARIES
 if $vexist(make_done)=0 then
   message "Load domains:" [domain]
   test = [domain]
   nw = $words([test])
   do i = 1, [nw]
     mod = $word([test],[i],1)
     n = $words([mod],'/') 
     module = $word([mod],[n],[n],'/')
     test_done = [module]_done
     if ($vexist([test_done])=0) then
       vec/cre [test_done]
       if ([module]=geometry) then
            exec  set_hadr_phys_on
            Mode TRAC  SIMU 1  | save secondaries which produced TPC hits
            detp TRAC  DCAY 210 210 0.1 0.01
            mode tpce  simu 2
            mode svtt  simu 2
            mode ftpc  simu 2
            detp svtt  svtg.nlayer = 6 | switch off shell 4
            ghist star.his  STAR
            gstat time size mult stak
        endif
        make [mod]
        if $iquest(1)<>0 then
            message "<bfc> Shared libraries have not been loaded : ABend"
         endif
     endif
     if $iquest(1)<>0 then
       message "<bfc> Shared libraries have not been loaded : ABend"
     endif
   enddo
   vec/cre make_done
 endif
return
 
**********************************************************************
**********************************************************************
**********************************************************************
 
macro bfc_init _
    chain='g2t svg tpg srs tss tcl sgr tpt svm egr  ev0 evout'
 
 macro/global/import *
    
 test = [chain]
 nw = $words([test])
 message ' !!! bfc_util ==> Number of words in chain = ' [nw]
 
 do i = 1, [nw]
   mod = $word([test],[i],1)
   message ' !!! bfc_util ==> chain initialization for  ' [mod]
   vec/create v[mod]
   vec/create t[mod]
   CASE [mod] in 
   (gethij) 
           newfile hij_in [input_file] r; 
           user/input u evgen/particle.staf
   (gst)   if ($vexist(geometry_done)=0) then
             exec [TOP]/kumacs/chain/bfc_util#bfc_start domain=geometry
           endif
    (fzd)  user/output o [output_fzd_file]
    (g2t)   cd [geant];
            dio/newfilestream G2Tfile [input_file] r | open file & read header 
            getev G2Tfile         | get Run
            if (STAF_STATUS(1).ne. STAFCV_OK) then
              message 'problem opening input file '
            goto EOF 
            endif
    (evout) if output_file="" then
              message "empty output file"
            else
              newfile evout [output_file] w     | open new output file
              if (STAF_STATUS(1).ne. STAFCV_OK) then
                message "Cannot open output file"
                goto eof
              endif
              if ($vexist(geometry_done)=1) then
                dio/stream/putevent evout [run]              
              endif
            endif
      (svg) exec init_svg         | SVT initialization - geometry
      (tpg) exec tpg_init#init    | TPC geometry initialization
      (sss) exec svt_slow_sim#initialize | SVT initialization - slow simulation
      (srs) exec init_srs	| SVT initialization - fast simulation
      (tfs) exec tpc_clustering#initialize_fast; | tpc fast simulation
            exec tpc_fast_sim#initialize;        | tpc fast simulation
      (tss) exec tpc_slow_sim#initialize _       
	       [adcxyzon] [tpc_sector_first] [tpc_sector_last]; | tpc slow sim
      (tcl) exec tpc_clustering#initialize
      (tpt) exec tpc_tracker#initialize
      (tte_t) exec tpc_tracker#initialize
      (tte_e)   
              if $index([test],tfs)>0 then
                exec tpc_evaluation#initialize
              else
                message ' NO evaluation for tpc slow sim'
              endif
      (stk) exec svt_tracker#initialize
      (sgr) exec svt_tracker#initialize
      (svm) exec init_svm
      (egr) exec init_egr
      (ev0) exec init_ev0#init_ev0
(ev0_eval2) exec init_ev0#ev0_eval2 
      (emc) exec init_ems;  
            exec init_jet;     |  EMC geometry &  pam initialization 
      (*)   message " !!! bfc_util: No initialization for package " [mod]
    ENDCASE
    
  enddo
 EOF:
 return
 
**********************************************************************
**********************************************************************
**********************************************************************

 macro bfc_process _
   chain='g2t svg tpg srs tss tcl sgr tpt svm egr  ev0 evout'
   
  macro/global/import *
  test = [chain]
  nw = $words([test])
  
  do i = 1, [nw]
   mod = $word([test],[i],1)
   message ' !!! bfc_util ==> chain processing for  ' [mod]
   CASE [mod] in 
      (gethij)    cd [event]; getev hij_in
               user/input u evgen/particle.staf
      (gst)    trig 1; cd [geant]; for/call g2t; cd /dui; 
      (g2t)    dio/stream/getevent G2Tfile [geant];
               if (staf_status(1).ne.STAFCV_OK) goto EOF
      (svg)    nextl 1
      (tpg)    nextl 1
      (sss)    exec svt_slow_sim#process_event;
      (srs)    exec svt_fast_sim#process_event; 
      (tfs)    exec tpc_fast_sim#process_event;
               if (staf_status(1) .ne. stafcv_ok) then 
                  message ' problem with tpc_fast_sim_process_event'
		endif
      (tss)    exec tpc_slow_sim#process_event _
		 [ievent] [adcxyzon] [tpc_sector_first] [tpc_sector_last]
               if (staf_status(1) .ne. stafcv_ok) then 
                 message ' problem with tpc_slow_sim_process_event'
               endif
      (tcl)    exec tpc_clustering#process_event _
		 [ievent] [tpc_sector_first] [tpc_sector_last]
               if (staf_status(1) .ne. stafcv_ok) then 
                 message ' problem with tpc_clustering_process_event'
               endif
      (emc)    exec run_ems_interface          
      (stk)    exec svt_tracker#process_event_stk [ievent];
               if (staf_status(1) .ne. stafcv_ok) then
                 nextl 1;
               endif
      (sgr)    exec svt_tracker#process_event_sgr [ievent];
               if (staf_status(1) .ne. stafcv_ok) then
                 nextl 1;
               endif
      (tpt)    exec tpc_tracker#process_event_tpt [ievent];
               if (staf_status(1) .ne. stafcv_ok) then 
                 message ' problem with tpc_tracker_tpt'
               endif
      (tte_t)
                 exec tpc_tracker#process_event_tte [ievent];
                 if (staf_status(1) .ne. stafcv_ok) then 
                   message ' problem with tpc_tracker_tte'
                 endif
      (tte_e)
               if $index([test],tfs)>0 then
	          exec tpc_evaluation#process_event [ievent];
               else
                  NO track evaluation for tss!
               endif
      (svm)    exec run_svm#run_svm 2	|  TPC to SVT Matching 
      (egr)    exec run_egr#egr 2             |  Event Global Refit
      (ev0)    exec run_ev0#ev0_am  2         |  V0 Finder 
      (ev0_eval2)    exec run_ev0#ev0_eval2  [ievent]
      (erj)    exec run_jet_egrid
               exec run_jet_erj
      (evout)  if [output_file]="" then
                 message "Empty output file"
               else
                 dio/stream/putevent evout [event]
               endif
      (*)      message " !!! bfc_util: No process stage for package " [mod]
      if (staf_status(1) .ne. stafcv_ok) goto NEXT_EVENT
   ENDCASE
	     
 sigma v[mod] = v[mod] + abs($cptime)
 sigma t[mod] = t[mod] + v[mod]
 ccc = '---'
 fmt = [ccc]//'<bfc>: Event  '//[ievent]//[ccc]//[mod]//' =' 
 fmt = $quote($quote([fmt]))//',2f12.5'
 vec/wr  v[mod],t[mod] ! [fmt]
 vec/inp v[mod] 0.0
   
enddo
NEXT_EVENT:
EOF:
return
 
**********************************************************************
**********************************************************************
**********************************************************************

macro bfc_finish _
   chain='g2t svg tpg srs tss tcl sgr tpt svm egr  ev0 evout'
  
  macro/global/import *
  test = [chain]
  nw = $words([test])
  do i = 1, [nw]
    mod = $word([test],[i],1)
    CASE [mod] in 
      (gethij) dio/stream/close hij_in; 
               soc/object/oid hij_in; 
               oid = staf_result(1); 
	       soc/deleteoid [oid] 
      (gst)   ge/rz/cd //PAWC; 
	       hi/list;		| show histograms
      (g2t)  dio/stream/close G2Tfile; 
             soc/object/oid G2Tfile; 
             oid = staf_result(1); 
             soc/deleteoid [oid]
      (evout) dio/stream/close evout;   
              soc/object/oid evout; 
              oid = staf_result(1); 
              soc/deleteoid [oid] 
      (*) message " !!! bfc_util: No final stage for package " [mod]
    ENDCASE
  enddo
return
 
**********************************************************************
**********************************************************************
**********************************************************************
 
macro bfc_print chain='evout'
  macro/global/import *
  test = [chain]
  nw = $words([test])
  do i = 1, [nw]
    mod = $word([test],[i],1)
    ccc = '---'
    fmt = '<bfc>: Event'//[ievent]//[ccc]//[mod]//[ccc]//' =' 
    fmt = $quote($quote([fmt]))//',2f10.5'
    vec/wr  v[mod],t[mod] ! [fmt]
  enddo
return
 
**********************************************************************
**********************************************************************
**********************************************************************

macro bfc_defaults
 macro/global/import *
 DEFAULT = _
[TOP]/kumacs/sim,_
[TOP]/kumacs/util,_
[TOP]/kumacs/chain,_
[TOP]/kumacs/emc,_
[TOP]/kumacs/svt,_
[TOP]/kumacs/tpc,_
[TOP]/pams/emc/kumac,_
[TOP]/pams/global/kumac,_
[TOP]/pams/svt/kumac,_
[TOP]/pams/tpc/kumac,_
$STAR/kumacs/sim,_
$STAR/kumacs/util,_
$STAR/kumacs/chain,_
$STAR/kumacs/emc,_
$STAR/kumacs/svt,_
$STAR/kumacs/tpc,_
$STAR/pams/ctf/kumac,_
$STAR/pams/emc/kumac,_
$STAR/pams/ftpc/kumac,_
$STAR/pams/gen/kumac,_
$STAR/pams/global/kumac,_
$STAR/pams/mwc/kumac,_
$STAR/pams/svt/kumac,_
$STAR/pams/tpc/kumac,_
$STAR/pams/trg/kumac

 def [DEFAULT]
 message 'Define default kumac search path as' [default]
 
return

**********************************************************************
**********************************************************************
**********************************************************************

















