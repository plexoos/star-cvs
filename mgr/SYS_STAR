#!/bin/csh
#  $Log: SYS_STAR,v $
#  Revision 1.3  1998/03/09 14:36:32  fisyak
#  Switch varibales
#
#  Revision 1.2  1998/02/10 00:06:09  fisyak
#  SL98a second version
#
#  Revision 1.1  1998/01/31 23:32:52  fisyak
#  New Environment variables
#
#  Revision 1.2  1998/01/30 12:42:15  fisyak
#  Save changes before moving to SL97b
#
#  Revision 1.1.1.1  1997/12/31 14:35:23  fisyak
#
#             Last modification $Date: 1998/03/09 14:36:32 $ 
#
# Determine SYS_STAR variable.
#
if (-e /usr/afsws/bin/sys) then
      setenv SYS_STAR `/usr/afsws/bin/sys`
      switch ($SYS_STAR)
	case "hp_ux102":
	    if (-e /bin/CC) then 
		setenv SYS_HOST_STAR "${SYS_STAR}"
	    else
		if (-e /opt/aCC/bin/aCC) then
		    setenv SYS_HOST_STAR "${SYS_STAR}_aCC"
		else
		    setenv SYS_HOST_STAR "${SYS_STAR}_gcc"
		endif
	    endif
	default:
      endsw
else
set mach_os = ` uname -sr | sed -e 's/ //g' -e 's/\.//g' `
#
switch ($mach_os)
case "AIX1":
	setenv SYS_STAR rs_aix31
	breaksw
case "AIX2":
	setenv SYS_STAR rs_aix32
	breaksw
case "HP-UXB1020":
	setenv SYS_STAR hp_ux102
if (-e /bin/CC) then 
  setenv SYS_HOST_STAR "${SYS_STAR}"
else
if (-e /opt/aCC/bin/aCC) then
  setenv SYS_HOST_STAR "${SYS_STAR}_aCC"
else
  setenv SYS_HOST_STAR "${SYS_STAR}_gcc"
endif
endif
	breaksw
case "IRIX4*":
case "IRIX52":
	setenv SYS_STAR sgi_52
	breaksw
case "IRIX53:"
	setenv SYS_STAR sgi_53
	breaksw
case "IRIX63":
	setenv SYS_STAR sgi_63
	breaksw
case "IRIX6464":
	setenv SYS_STAR sgi_64
	breaksw
case "Linux*":
	setenv SYS_STAR linux
	breaksw
case "OSF1V32":
case "OSF1V40":
	setenv SYS_STAR alpha_osf32c
	breaksw
case "OSF1*":
	setenv SYS_STAR osf1
	breaksw
case "SunOS54"
        setenv SYS_STAR sun4x_54
	breaksw
case "SunOS551"
        setenv SYS_STAR sun4x_55
	breaksw
case "SunOS5*":
	switch ("`uname -p`")
	case "*86":
		setenv SYS_STAR sunx86_55
		breaksw
	default:
		setenv SYS_STAR sun4x_55
		breaksw
	endsw
	breaksw
default:
	setenv SYS_STAR unknown
	breaksw
endsw
endif
#echo "mach_os = " $mach_os
if ($?SYS_HOST_STAR == 0) setenv SYS_HOST_STAR $SYS_STAR
