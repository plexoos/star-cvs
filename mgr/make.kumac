macro make 
if [#]>0 then
for pam in [*]
  t = $word([pam],1,1,'/')
  if $index([t],'pams')<1 then
    p = pams/[pam]
  else
    p = [pam]
  endif 
  exec makes pam=[p]
endfor
else
for pam in emc global mwc sim svt vpd ctf ftpc geometry magnet tpc
  exec makes pam=pams/[pam]
endfor
endif
return
**************************************************
macro makes pam=pams/tpc makefile=$STAR/mgr/Makefile
cut = ' \( -type d -a ! -name CVS -a ! -name idl _
                   -a ! -name exa -a ! -name inc _
                   -a ! -name "src/*" _
                   -a ! -name doc _
                   -a ! -name . -a ! -name wrk \) -prune -print'
list = ''
if $fexist([pam])=1 then
list = $shell('find '//[pam]//[cut]);
endif
listr= $shell('find $STAR/'//[pam]//[cut]);
exec load [list] [listr] 
ami/list
return
**************************************************
macro load 
n1 = $words([1],' ')
n2 = $words([2],' ')
i1 = 1 
cwd1 = '????'
if [n1]>0 then
  cwd1 = $word([1],[i1],1)
endif
do i2 = 1, [n2]
  cwd2 = $word([2],[i2],1)
  ind  = $index([cwd2],[cwd1]) 
  if [ind]=0 then
    exec ld_standard [cwd2]
  elseif [i1]<=[n1] then 
    exec ld_private  [cwd1]
    i1 = [i1]+1
    cwd1 = $word([1],[i1],1)
  endif
enddo
do i= [i1], [n1]
  cwd1 = $word([1],[i],1)
  exec ld_private  [cwd1]
enddo 
return
**************************************************
macro ld_standard
  pam = [1]
  n = $words([pam],'/') 
  p = $word([pam],[n],[n],'/')
  slib = $STAR_LIB/[p].sl
  sl = $fexist([slib])
  if [sl]=1 then
     message  '================ Load standard shared library:' [slib] '================'
     plib = $STAR_LIB/[p]
*     call  csrmsl($quote([plib])); for/call [p]_init;
     gexec [slib]; for/call [p]_init;
  endif
return
**************************************************
macro ld_private
  pam = [1]
  n = $words([pam],'/') 
  p = $word([pam],[n],[n],'/')
*  plib = $STAR_LIB/[p]
*  call  csrmsl($quote([plib]))
  shell gmake SILENT=YES -f $STAR/mgr/MakePam.mk -C [pam]
  slib = lib/$STAR_SYS/[p].sl
  sl = $fexist([slib])
  if [sl]=1 then
    message  '================ Load private shared library:' [slib] '================'
    plib = $STAR_LIB/[p]
*    call  csrmsl($quote([plib])); for/call [p]_init;
    gexec [slib]; for/call [p]_init;
  endif
return
endkumac
**************************************************
macro gload
on error exitm
call  csrmsl($quote([1]))
*shell gmake [1].sl
call  [1]
*gexec [1]
exitm
