#!/bin/csh -f
# 
# auau_cent :  	example t/csh shell script for running VENUS
#
#		3 Central Au+Au events, RHIC energy,
#		fzd format,
#		rescattering on
#
# Created:	Matthew Bloomer  4/21/95
# Modified:	Matthew Bloomer	 6/ 8/95
#               Bruce Moskowitz  5/21/96 (from Stratos Efstathiadis)
# Usage:  
#	cd $STAR_ARCH
#	../auau_cent >&! auau_cent.log &
#
#-------------------------------------------------------------
#
# Source star_login.csh in case STAR environment variables are
# not setup (e.g. batch mode on SP1).
#
source /afs/rhic/star/login/star_login.csh
#
# Other environment definitions:
#   	XFLRTEOPTS : 	use "old" namelist convention
setenv XFLRTEOPTS namelist=old
setenv VENUS_VERSION "412"
#
# Remove previously existing files from VENUS running
#
if (-e check) rm check
if (-e data)  rm data
if (-e optns) rm optns
if (-e histo) rm histo
rm -f venus_egz.data
#
# generate input parameter file $params_file
# (make changes to parameters here)
# 
set params_file = optns
set output_file = $DST_ROOT/data5/$USER/venus/ven_auau_cent.fzd
set exec_file   = $STAR_LIB/$STAR_SIM_LEVEL/sim/bin/venus_$VENUS_VERSION
#
# produce runcards for VENUS running.  (See documentation in 
# ven/doc/venus_412.doc).  AA collisions use 'had' card.
#
echo "'had'"				>>$params_file
#
# 3 events, update message every 1 events, 200 AGeV (NN center of
# mass system specified by negative value), Au+Au.
# Last parameter:  specifies how many parameter option lines follow 
# first line.  Don't forget to set it correctly!!!
#
echo "3 1 -200.0 79 197 79 197 6"	>>$params_file
#
# Do we want to store also the parents?: NO (Venus does not
# work with istmax=1).
#
echo "'istmax' 0"			>>$params_file
#
# Do we decay all resonances in VENUS?:  SUPPRESS decay of long lived 
# resonances
#
echo "'ndecay' 1111110"			>>$params_file
#
echo "'ndecax' 0010001"			>>$params_file
#
# Set maximum impact parameter (in fermi):
#
echo "'bmaxim'   1"			>>$params_file
#
# seed value:  replace for each new set of events
#
echo "'seedi'  108236"			>>$params_file
#
if ($STAR_ARCH == "aix") then
#
# KLUDGE:  KUIP prompt for frame of reference seems to want to read
# from logical unit 5 in AIX, which unfortunately is the optns file.
# This is in spite of any redirection at the command line. 
# Consequently, add parameter 'CMS' here to optns file, since this
# is where it is prompted for when writing out first event.  
# This crummy kludge needs to worked out correctly someday.
#
  echo "CMS"				>>$params_file
#
endif
#
# obligatory stop cards card
#  outfgdt directs output flow.ifgdt=1 is standard ZEBRA output(matt bloomer) 
#                              ifgdt=2 is standard text output(richard morris)
#                              ifgdt=3 is gstar text output (peter jacobs)
#
echo "'ifgdt '   1"		>>$params_file
#
echo "'stp'"		        >>$params_file
#
# Setup symbolic line to data file (VENUS writes to 
# default file venus_egz.data).  Remove example data file
# if already present.
#
if (-e $output_file) rm $output_file
ln -s $output_file venus_egz.data
#
# Run VENUS:  first parameter defines frame of reference to output
# momenta ('CMS' or 'PROJ') (except for AIX:  see KLUDGE above).
#
if ($STAR_ARCH == "aix") then
  time $exec_file < $params_file   # this is dummy means of redirection
else
  time $exec_file <<eof
CMS
eof
#
endif
#
# End of running
#
echo "That's it folks"
exit