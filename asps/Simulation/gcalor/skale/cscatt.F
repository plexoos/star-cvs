* $Id: cscatt.F,v 1.1.1.1 2004/01/15 00:08:49 potekhin Exp $
* $Name:  $
#include <commons/config.h>
      SUBROUTINE CSCATT(INC,EKE1,ITFAS,EFAS,ALPFAS,BETFAS,GAMFAS)
C
C
C      INC = TYPE OF INCIDENT PARTICLE
C               = 0  PROTON
C               = 1  NEUTRON
C               = 2  PI +
C               = 3  NOT USED
C               = 4  PI -
C      EKE1 = KINETIC ENERGY OF INCIDENT PARTICLE(MEV)
C      ITFAS = TYPE OF PARTICLE (SAME AS ABOVE)
C      EFAS = KINETIC ENERGY OF PARTICLES (MEV)
C      ALPFAS = X-DIRECTION COSINE
C      BETFAS = Y-DIRECTION COSINE
C      GAMFAS = Z-DIRECTION COSINE
C      RANDS = LOCATION OF RANDOM NUMBER SEQUENCE
C
C               SUBROUTINES CALLED CAAZIO.
C
      DIMENSION ITFAS(2),EFAS(2),ALPFAS(2),BETFAS(2),GAMFAS(2),RANDS(4)
      REAL*8 B,M1,M2,E1,P1,ECM1,PCM1,ETLAB,ETCM,BETA,GAMMA,T1,T2,T3,T4,
     1 T5,T6,T7,T8,T9,T10,T11,T12,PLAB1,PLAB2,CST,SNT,TB
#include "geant321/crandm.inc"
C
      DATA M2/940.075D0/
      SAVE
C
      T2 = M2*M2
      IN=INC+1
      GO TO (10,10,20,30,40),IN
   10 M1=940.075D0
      GO TO 60
   20 B=7.040D-6
      GO TO 50
   30 CALL CERROR('SCATT1')
   40 B=7.575D-6
   50 M1=139.89D0
   60 E1=EKE1+M1
      T1=M1*M1
      P1=DSQRT(E1*E1-T1)
      IF(IN.GT.2) GO TO 70
      B=7.26D-6+ (3.13D-11)*P1
   70 ETLAB=E1+M2
      BETA=P1/ETLAB
      GAMMA=1.D0/DSQRT(1.D0-BETA*BETA)
      ETCM=ETLAB/GAMMA
      T3=T1+T2
      T4=T1-T2
      T4=T4*T4
      T5=ETCM*ETCM
      T6=T5*T5
      T7=B*(T6-2.D0*T5*T3+T4)/(2.D0*T5)
      T8 = RANDC(ISEED)
      TB=T7
      IF(T7.GT.50.D0) TB=50.D0
      CST=1.D0+(DLOG(1.D0-T8*(1.D0-DEXP(-TB))))/T7
      SNT=DSQRT(1.D0-CST*CST)
      CALL CAAZIO(T9,T10)
      ECM1=(T5+T1-T2)/(2.D0*ETCM       )
      PCM1=DSQRT(ECM1*ECM1-T1)
      ITFAS(1)=INC
      ITFAS(2)=0.
      T11    = GAMMA*(ECM1+PCM1*CST*BETA) - M1
      T12 = ETLAB - T11 -M1 - M2
      PLAB1 = DSQRT(2.D0*M1*T11 + T11*T11)
      PLAB2 = DSQRT(2.D0*M2*T12 + T12*T12)
      EFAS(1) = T11
      EFAS(2) = T12
      ALPFAS(1)=PCM1*SNT*T9
      ALPFAS(2)= -ALPFAS(1)
      ALPFAS(1)=ALPFAS(1)/PLAB1
      ALPFAS(2)=ALPFAS(2)/PLAB2
      BETFAS(1)=PCM1*SNT*T10
      BETFAS(2)=-BETFAS(1)
      BETFAS(1)=BETFAS(1)/PLAB1
      BETFAS(2)=BETFAS(2)/PLAB2
      GAMFAS(1)= GAMMA*(PCM1*CST+BETA*ECM1)
      GAMFAS(2)= -GAMFAS(1)+P1
      GAMFAS(1)= GAMFAS(1)/PLAB1
      GAMFAS(2)= GAMFAS(2)/PLAB2
      RETURN
      END
