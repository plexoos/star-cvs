* $Id: agnzgetp.age,v 1.1.1.1 2004/01/12 23:49:37 potekhin Exp $
* $Log: agnzgetp.age,v $
* Revision 1.1.1.1  2004/01/12 23:49:37  potekhin
*
* Revision 1.1  2001/02/27 10:14:10  nevski
*  first working release
*
****************************************************************************
    SUBROUTINE AgNZGETP(IL,IDE,IP,ISTAT,IDPDG,P,AMASS,MOTH,TIME,IDAU1,V)
* Description: Get parameters of a single particle from EVNT bank          *
* Input : IL    - Link number : 1 = primary, 2 = secondary (obsolete)      *
*         IDE   - ID of event in gate                                      *
*         IP    - Number of particle in event record                       *
* Output: ISTAT - HEPEVT status flag. Returns 0 if record not found.       *
*         IDPDG - PDG code for particle                                    *
*         P     - 4-momentum (px,py,pz,E)                                  *
*         AMASS - particle (quark) mass                                    *
*         MOTH  - mothers. If MOTH(2)<0 the range is MOTH(1)-ABS(MOTH(2))  *
*         TIME  - Start time of particle relative to interaction (sec)     *
*         IDAU1 - a daughter pointer                                       *
*         V     - vertex coordinates (in meter)                            *
* Original version:                              Robert DeWolf  23/07/92   *
****************************************************************************
#include "commons/typing.inc"
#include "geant321/gcbank.inc"
#include "slug/sclink.inc"
      Character Cbank*4
      REAL      P(4),AMASS,PTOT,VMOD,TIME,V(3)
      INTEGER   LK,IL,IDE,IP,IDPDG,ISTAT,MOTH(2),IDAU1,
                LGENE,LGENP,LZFIND,MODUL,ND,IOFF
*
      entry gNZGETP(IL,IDE,IP,ISTAT,IDPDG,P,AMASS,MOTH,TIME,IDAU1,V)
      IDPDG = 0;       ISTAT = 0;                   Check   LkEvnt>0
*
* Find the event with the desired IDN:
*
      Lk=LZFIND(IXDIV,LkEvnt,IDE,-5);               Check   Lk>0
      check  "#of links" IQ(Lk-2)>=2
      LGENE = LQ(Lk-2);                             Check   LGENE>0
      check  "bank length" IQ(LGENE-1)>=12 & "#of links" IQ(LGENE-2)>=1
      LGENP = LQ(LGENE-1);                          Check   LGENP>0
      ND    = IQ(LGENP-1)
      Call UHTOC(IQ(LGENP-4),4,Cbank,4)
      if     Cbank=='GENP'
      {
         MODUL   = MOD(IQ(LGENP+3),100 000)
         IOFF    = MODUL * (IP-1) + 3
         Check     IOFF+MODUL<=ND
         LGENP   = LGENP + IOFF
*
         ISTAT   = IQ(LGENP + 1)
         IDPDG   = IQ(LGENP + 2)
         MOTH(1) = IQ(LGENP + 3)
         MOTH(2) = IQ(LGENP + 4)
         IDAU1   = IQ(LGENP + 5)
         call UCOPY(Q(LGENP + 6),P,3)
         AMASS   =  Q(LGENP + 9)
         TIME    =  Q(LGENP + 10)
         call UCOPY(Q(LGENP + 11),V,3)
         PTOT    = VMOD( Q(LGENP+6),3)
         IF AMASS>=0. { P(4) = SQRT(PTOT**2  +  AMASS**2)     }
         ELSE         { P(4) = SQRT(MAX(PTOT**2-AMASS**2,0.)) }
      }
      elseif Cbank=='GENT'
      {
         IOFF    = (IP-1)*15
         CHECK     IOFF+15 <= ND
         LGENP   = LGENP + IOFF
*
         ISTAT   = IQ(LGENP + 1)
         IDPDG   = IQ(LGENP + 2)
         MOTH(1) = IQ(LGENP + 3)
         MOTH(2) = IQ(LGENP + 4)
         IDAU1   = IQ(LGENP + 5)
         call UCOPY(Q(LGENP + 7),P,4)
         AMASS   =  Q(LGENP + 11)
         call UCOPY(Q(LGENP + 12),V,3)
         TIME    =  Q(LGENP + 15)
      }
      else  { Print *,'AgNZGETP: unknown bank',Cbank }
      END



