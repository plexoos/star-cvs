*
* $Id: agshape.age,v 1.1.1.1 2004/01/12 23:49:37 potekhin Exp $
* $Name:  $
* $Log: agshape.age,v $
* Revision 1.1.1.1  2004/01/12 23:49:37  potekhin
*
* Revision 1.1  2001/02/27 10:14:35  nevski
*  first working release
*
*
#include <atlsim/config.h>
**********************************************************************
*                                                                    *
                    Subroutine   A g S H A P E
*                                                                    *
*  Description: given SHAPE or ISHAPE  returns its code/name and     *
*               parameters                                           *
*  Modifications:                                                    *
*  A.Dellacqua 27.02.97: CTUB parameters introduced                  *
**********************************************************************
#include "commons/typing.inc"
#include "commons/agecom.inc"
#include "geant321/gcunit.inc"
Replace[;#?#;]    with  [[SETR a=0];    IF %Shape=='#1' {;#2=>Par;};          ]
Replace[;#,#=>#;] with  [{IF} [EXIST 1] {[INCR a]; %#3([COPY a])=%#1; #2,=>#3;}
                         {ELSE} {; %Npar=[COPY a]; }            ]

 Integer  LENOCC,Is,I,K; Character*4  ShapS(30);
 Data ShapS /'BOX ','TRD1','TRD2','TRAP','TUBE','TUBS','CONE','CONS','SPHE',
      'PARA','PGON','PCON','ELTU','HYPE',  13 * '    ','GTRA','CTUB','    '/;

 If %Ishape<=0  { Do Is=1,30 { IF %SHAPE==ShapS(Is)  { %Ishape=Is; Break;} } }
 If %Ishape<=0  { %error(' Undefined shape ',%SHAPE)}   %Shape=ShapS(%Ishape);

 Box  ?  dx,dy,dz;
 Trd1 ?  dx1,dx2, dy,dz;
 Trd2 ?  dx1,dx2, dy1,dy2, dz;
 Trap ?  dz,thet,phi, h1,bl1,tl1,alp1, h2,bl2,tl2,alp2;
 Tube ?  Rmin,Rmax,Dz;
 Tubs ?  Rmin,Rmax,Dz, Phi1,Phi2;
 Cone ?  Dz, Rmn1,Rmx1, Rmn2,Rmx2;
 Cons ?  Dz, Rmn1,Rmx1, Rmn2,Rmx2, phi1,phi2;
 Sphe ?  Rmin,Rmax, The1,The2, Phi1,Phi2;
 Para ?  Dx,Dy,Dz,  Alph,Thet,Phi;
 Pgon ?  Phi1,Dphi,Npdiv,Nz;
 Pcon ?  Phi1,Dphi,      Nz;
 Eltu ?  P1, P2, Dz;
 Hype ?  Rmin,Rmax,Dz,Thet;
 Gtra ?  Dz,Thet,Phi,Twis, h1,bl1,tl1,alp1, h2,bl2,tl2,alp2;
 Ctub ?  Rmin,Rmax,Dz, Phi1,Phi2, Lx,Ly,Lz,Hx,Hy,Hz;

 If %Shape=='PGON' | %Shape=='PCON'
 { k=%Npar; %Npar+=3*%Nz;  do i=1,%Nz
   { %Par(3*i+k-2)=%zi(i); %Par(3*i+k-1)=%rmn(i); %Par(3*i+k)=%rmx(i);}
 }
   END

