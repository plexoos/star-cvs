*
* $Id: agsdivi.age,v 1.1.1.1 2004/01/12 23:49:37 potekhin Exp $
* $Name:  $
* $Log: agsdivi.age,v $
* Revision 1.1.1.1  2004/01/12 23:49:37  potekhin
*
* Revision 1.1  2001/02/27 10:14:34  nevski
*  first working release
*
*
#include <atlsim/config.h>
**********************************************************************
*                                                                    *
                    Subroutine   A g S D I V I
*                                                                    *
*  Description: make volume division according to actual parameters  *
*                                                                    *
**********************************************************************
#include "commons/typing.inc"
#include "commons/agecom.inc"
#include "geant321/gcbank.inc"
#include "geant321/gcunit.inc"
#include "geant321/gcnum.inc"
 Character   Fun*6,Mother*4
 Integer     LENOCC,Name,Nick,Nin,Jmo,Jvd,Ndm,JATTF,Jj,Jat
 JATTF(Jj) = Jj+int(Q(Jj+5))+6
*
 Call UCTOH(%Volume,Name,4,4)
 Call UHTOC(IQ(JVOLUM+%Imother),4,Mother,4)
 Jmo=LQ(JVOLUM-%Imother);  nin=Q(Jmo+3);
 If nin>0  { %error('Volume has daughters, division impossible')}
 If nin<0  { %Ivolume=Q(LQ(Jmo-1)+2);       Jvd=LQ(JVOLUM-%Ivolume);
             If IQ(Jvd-4)#NAME  { %error('bad name of an existing division')}
             Call UHTOC(IQ(JVOLUM+%Ivolume),4,%CNICK,4)
             %Ignum=IQ(Jvd-5);              Return;
           }
 %Ignum=0;
 Do %Ivolume=1,NVOLUM { If(IQ(LQ(JVOLUM-%Ivolume)-4)=Name) %Ignum+=1; }
 Call AgSNAME (%Volume,%Ignum,%CNick)
*
  Ndm=%NdvMax;
  If      %C0=0 & %Ndiv#0
  { Fun='GSDVN';  Call GSDVN (%Cnick,Mother,%Ndiv,%Iaxis )}
  else If %C0=0 & %Step#0
  { Fun='GSDVT';  Call GSDVT (%Cnick,Mother,%Step,%Iaxis,%Imed,Ndm)}
  else If %C0#0 & %Ndiv#0 & %Step#0
  { Fun='GSDVX';  Call GSDVX (%Cnick,Mother,%Ndiv,%Iaxis,%Step,%C0,%Imed,Ndm)}
  else If %C0#0 & %Ndiv#0
  { Fun='GSDVN2'; Call GSDVN2(%Cnick,Mother,%Ndiv,%Iaxis,%C0,%Imed)}
  else If %C0#0 & %Step#0
  { Fun='GSDVT2'; Call GSDVT2(%Cnick,Mother,%Step,%Iaxis,%C0,%Imed,Ndm)}
  else { %error('missing parameters to make a division ')}
*
  Call UCTOH(%Cnick,Nick,4,4)
  If IQ(JVOLUM+Nvolum)#Nick {%error('can not find newly created division ')}
  "set also volume generic name and number in IDH & IDN, and IdType "
  %Ivolume=NVOLUM; Jvd=LQ(JVOLUM-%Ivolume)
  IQ(Jvd-4)=NAME;  IQ(Jvd-5)=%Ignum;
  Jat=JATTF(Jvd);  Q(jat+9)=%IdType;  Q(jat+10)=%Serial;
  prin1 Mother,%Cnick,%L(%Volume),%Ignum,Fun
  (' AGSDIVI: mother ',A4,' divided into ',A4,2x,A4,'(',i3,') using ',A6)
  END


