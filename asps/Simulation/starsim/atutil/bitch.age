* $Id: bitch.age,v 1.1.1.1 2004/01/12 23:49:38 potekhin Exp $
* $Log: bitch.age,v $
* Revision 1.1.1.1  2004/01/12 23:49:38  potekhin
*
* Revision 1.1  2001/02/27 10:15:01  nevski
*  first working release
*
C----------------------------------------------------------------------
      FUNCTION  BITCH(INT,FORMAT)
C
C   Functional Description :
C BITCH Converts a 32 bit integer, INT, into either 8 hexadecimal
C or 11 octal characters (according to the value of FORMAT) and
C returns as the value of BITCH as many characters as will fit
C from the right end of the string (Caller declares length of BITCH).
C If FORMAT is something other than 'H' or 'O' BITCH returns blank.
C
C   Input arguments
C      INTEGER   INT        ! actually a bit string, assumed 32 bits long
C      CHARACTER FORMAT     ! either 'H' or 'O'.
C   Returned value : ASCII string of digits representing INT.
C
C Converted to STAR style  5 Feb 1992   Bill Love
C----------------------------------------------------------------------
      IMPLICIT   NONE
      CHARACTER  BITCH*(*)
      INTEGER    INT        ! actually a bit string, assumed 32 bits long
      CHARACTER  FORMAT     ! either 'H' or 'O'.
      INTEGER    I,L,M3,M4
      CHARACTER  TAB(16),STRING*11
C-------
      DATA M3/7/,M4/15/
      DATA TAB/'0','1','2','3','4','5','6','7',
     >         '8','9','A','B','C','D','E','F'/
C-------
C
        STRING=' '
        IF(FORMAT.EQ.'H')THEN
            DO I=1,8
              STRING(I+3:I+3)=TAB(IAND(M4,ISHFT(INT,4*(I-8)))+1)
            ENDDO ! I loop to 8
        ELSE IF(FORMAT.EQ.'O')THEN
            DO I=1,11
              STRING(I:I)=TAB(IAND(M3,ISHFT(INT,3*(I-11)))+1)
            ENDDO ! I loop to 11
        ENDIF
        BITCH = STRING
        L     = LEN(BITCH)
        IF(L.LT.11) BITCH=STRING(12-L:11)
        RETURN
        END

