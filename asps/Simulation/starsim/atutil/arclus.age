* $Id: arclus.age,v 1.1.1.1 2004/01/12 23:49:38 potekhin Exp $
* $Log: arclus.age,v $
* Revision 1.1.1.1  2004/01/12 23:49:38  potekhin
*
* Revision 1.1  2001/02/27 10:15:00  nevski
*  first working release
*
************************************************************************
*                                                                      *
                subroutine    A R C L U S (Par,Key)
*                                                                      *
* description:  SMEAR CLOSE HITS. key>0 - 2 dimensional pixels         *
*                                 key<0 strips                         *
************************************************************************
#include "commons/typing.inc"
#include "commons/agcclus.inc"
*
  INTEGER    Itype,Key,MM,MY,MZ,ID,JD,IT,IND,JND,NN
  Real       Par(3),Dfp,Dzp,SE,SY,SZ
*
    Itype = nint(Par(1))
    Dfp   = Par(2)
    Dzp   = Par(3)
    If Itype=1 { "pixel" If Dfp<Dzp { Key=1 } else {Key=2 }; MM=100000}
    else       { "s.s.s" If Dfp<Dzp { Key=-1} else {Key=-2}; MM=0 }

*   select direction of clustering
    IF Dfp<Dzp {"Phi measurement, strips or pads in z (or R)" MY=1; MZ=MM;}
    else       {"z (or R) measurement, strips or pads in Phi" MY=MM; MZ=1;}
*
    DO ID=1,Ndig
    {   Check Edig(Id)>0
        IND=nint(Fdig(ID)/dfp+0.5)*MY+nint(Zdig(ID)/dzp+0.5)*MZ;
        { NN,SE,Sy,Sz }=0;    IT=Ltra(ID);
        DO JD=ID,Ndig
        {  Check Edig(JD)>0
           JND=nint(Fdig(JD)/dfp+0.5)*MY+nint(Zdig(JD)/dzp+0.5)*MZ;
                                     "   check if hits are adjacent   "
           IF JND=IND
           {  NN+=1;            IND+=1;
              SE+=Edig(JD);     SY+=Fdig(JD);    SZ+=Zdig(JD);
              IF (IT!=Ltra(Jd)) IT=0;            Edig(JD)=0;
        }  }
        Edig(Id)=SE; Fdig(Id)=SY/NN; Zdig(Id)=SZ/NN; Nclu(Id)=NN; Ltra(Id)=IT;
    }
  END

